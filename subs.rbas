'OHRRPGCE CUSTOM - Misc unsorted routines
'(C) Copyright 1997-2005 James Paige and Hamster Republic Productions
'Please read LICENSE.txt for GPL License details and disclaimer of liability
'See README.txt for code docs and apologies for crappyness of this code ;)
'
#include "config.bi"
#include "const.bi"
#include "udts.bi"
#include "custom_udts.bi"
#include "allmodex.bi"
#include "common.bi"
#include "loading.bi"
#include "customsubs.bi"
#include "slices.bi"
#include "cglobals.bi"

#include "uiconst.bi"
#include "scrconst.bi"

#include "flexmenu.bi"

'Defined in this file:

DECLARE FUNCTION enemy_edit_add_new (recbuf() as integer, byref recindex as integer, preview_box as Slice Ptr) as integer
DECLARE SUB enemy_edit_update_menu(byval recindex as integer, state as MenuState, recbuf() as integer, menu() as string, menuoff() as integer, menutype() as integer, menulimits() as integer, min() as integer, max() as integer, dispmenu() as string, workmenu() as integer, caption() as string, byref preview_sprite as Frame ptr, preview as Slice Ptr, byval dissolve_ticks as integer, byval EnLimSpawn as integer, byval EnLimPic as integer, byval EnDatPic as integer, byval EnDatPal as integer, byval EnDatPicSize as integer)
DECLARE SUB enemy_edit_load(byval recnum as integer, recbuf() as integer, state as MenuState, caption() as string, byval EnCapElemResist as integer)
DECLARE SUB enemy_edit_pushmenu (state as MenuState, byref lastptr as integer, byref lasttop as integer, byref menudepth as bool)
DECLARE SUB enemy_edit_backmenu (state as MenuState, byval lastptr as integer, byval lasttop as integer, byref menudepth as bool, workmenu() as integer, mainMenu() as integer)

DECLARE SUB individual_formation_editor ()
DECLARE SUB formation_set_editor ()
DECLARE SUB draw_formation_slices(form as Formation, rootslice as Slice ptr, selected_slot as integer, page as integer)
DECLARE SUB load_formation_slices(ename() as string, form as Formation, rootslice as Slice ptr ptr)

DECLARE FUNCTION item_attack_name(n as integer) as string
DECLARE SUB generate_item_edit_menu (menu() as string, shaded() as bool, itembuf() as integer, item_name as string, info_string as string, equip_types() as string, byref box_preview as string)

DECLARE SUB item_editor
DECLARE SUB individual_item_editor(item_id as integer)
DECLARE SUB item_editor_equipbits(itembuf() as integer)
DECLARE SUB item_editor_elementals(itembuf() as integer)
DECLARE SUB item_editor_init_new(itembuf() as integer)
DECLARE SUB item_editor_stat_bonuses(itembuf() as integer)

DECLARE SUB enforce_hero_data_limits(her as HeroDef)
DECLARE SUB update_hero_appearance_menu(byref st as HeroEditState, menu() as string, her as HeroDef)
DECLARE SUB update_hero_preview_pics(byref st as HeroEditState, her as HeroDef)
DECLARE SUB animate_hero_preview(byref st as HeroEditState)
DECLARE SUB clear_hero_preview_pics(byref st as HeroEditState)
DECLARE SUB draw_hero_preview(st as HeroEditState, her as HeroDef)

DECLARE SUB hero_editor
DECLARE FUNCTION hero_editor_add_new (st as HeroEditState, her as HeroDef, byref hero_id as integer) as bool
DECLARE SUB hero_editor_load_hero (st as HeroEditState, her as HeroDef, byval hero_id as integer)
DECLARE SUB hero_editor_stats_menu (her as HeroDef)
DECLARE SUB hero_editor_spell_lists_toplevel (her as HeroDef)
DECLARE SUB hero_editor_edit_spell_list (her as HeroDef, byval listnum as integer)
DECLARE SUB hero_editor_spell_list_names (her as HeroDef)
DECLARE SUB update_hero_tags_menu (byref hero as HeroDef, menu() as string)
DECLARE SUB hero_editor_tags (byval hero_id as integer, byref hero as HeroDef)
DECLARE SUB hero_editor_appearance (byref st as HeroEditState, byref her as HeroDef)
DECLARE SUB hero_editor_equipment_list (byval hero_id as integer, byref her as HeroDef)
DECLARE SUB hero_editor_equipbits (byval hero_id as integer, byval equip_type as integer)
DECLARE SUB hero_editor_elementals(byref her as HeroDef)
DECLARE SUB hero_editor_edit_spell_list_slot (slot as SpellList)
DECLARE SUB hero_editor_battle_menu (her as HeroDef)
DECLARE SUB edit_battle_menu_item (hero as HeroDef, byval edit_node as NodePtr)
DECLARE SUB browse_battle_menu_kinds (byval menu_node as NodePtr)
DECLARE SUB change_battle_menu_kind (byval menu_node as NodePtr, new_kind as string, init_integer as bool=NO)
DECLARE SUB hero_editor_stat_visibility (hero as HeroDef)

' Formation editor slice lookup codes
CONST SL_FORMEDITOR_BACKDROP = 100
CONST SL_FORMEDITOR_ENEMY = 200  '+0 to +7 for 8 slots
CONST SL_FORMEDITOR_LAST_ENEMY = 299  'End of range indicating an enemy slot
CONST SL_FORMEDITOR_CURSOR = 300


SUB clearallpages
clearpage 0 'UPDATE as of fbc v0.20
clearpage 1 'I re-checked a loop version of this. It's still bigger.
clearpage 2 'Sure, it really doesn't matter, but just saying...
clearpage 3 '~Mike
END SUB

SUB update_enemy_editor_for_elementals(recbuf() as integer, caption() as string, byval EnCapElemResist as integer)
 FOR i as integer = 0 TO gen(genNumElements) - 1
  caption(EnCapElemResist + i) = format_percent(DeSerSingle(recbuf(), 239 + i*2))
 NEXT
END SUB

SUB enemydata

DIM elementnames() as string
getelementnames elementnames()

clearallpages

'-------------------------------------------------------------------------

'--bitsets
DIM ebit(64) as string

FOR i as integer = 32 TO 53
 ebit(i) = "" 'preferable to be blank, so we can hide it
NEXT i
ebit(54) = "Harmed by Cure"
ebit(55) = "MP Idiot"
ebit(56) = "Boss"
ebit(57) = "Unescapable"
ebit(58) = "Die Without Boss"
ebit(59) = "Flee instead of Die"
ebit(60) = "Untargetable by Enemies"
ebit(61) = "Untargetable by Heros"
ebit(62) = "Win battle even if alive"
ebit(63) = "Never flinch when attacked"
ebit(64) = "Ignored for ""Alone"" AI"

'-------------------------------------------------------------------------

'--record buffer
DIM recbuf(dimbinsize(binDT1)) as integer

CONST EnDatName = 0' to 16
CONST EnDatStealAvail = 17
CONST EnDatStealItem = 18
CONST EnDatStealItemP = 19
CONST EnDatStealRItem = 20
CONST EnDatStealRItemP = 21
CONST EnDatDissolve = 22
CONST EnDatDissolveTime = 23
CONST EnDatDeathSFX = 24
CONST EnDatCursorX = 25
CONST EnDatCursorY = 26
'27 to 52 unused
CONST EnDatPic = 53
CONST EnDatPal = 54
CONST EnDatPicSize = 55
CONST EnDatGold = 56
CONST EnDatExp = 57
CONST EnDatItem = 58
CONST EnDatItemP = 59
CONST EnDatRareItem = 60
CONST EnDatRareItemP = 61
CONST EnDatStat = 62' to 73
CONST EnDatBitset = 74' to 78
CONST EnDatSpawnDeath = 79
CONST EnDatSpawnNEDeath = 80
CONST EnDatSpawnAlone = 81
CONST EnDatSpawnNEHit = 82
CONST EnDatSpawnElement = 83' to 90
CONST EnDatSpawnNum = 91
CONST EnDatAtkNormal = 92' to 96
CONST EnDatAtkDesp = 97'   to 101
CONST EnDatAtkAlone = 102' to 106
CONST EnDatElemCtr = 107' to 114
CONST EnDatStatCtr = 115' to 126
CONST EnDatElemCtr2 = 127' to 182
CONST EnDatSpawnElement2 = 183' to 238
CONST EnDatElemResist = 239' to 366
CONST EnDatAtkBequest = 367

'-------------------------------------------------------------------------

DIM capindex as integer = 0
REDIM caption(-1 TO -1) as string
DIM max(26) as integer
DIM min(26) as integer
'Limit 0 is not used

CONST EnLimPic = 1
max(EnLimPic) = gen(genMaxEnemy1Pic) 'or 28 or 29. Must be updated!

CONST EnLimUInt = 2
max(EnLimUInt) = 32767

CONST EnLimPicSize = 3
max(EnLimPicSize) = 2
DIM EnCapPicSize as integer = capindex
addcaption caption(), capindex, "Small 34x34"
addcaption caption(), capindex, "Medium 50x50"
addcaption caption(), capindex, "Big 80x80"

CONST EnLimItem = 4
max(EnLimItem) = gen(genMaxItem)

CONST EnLimPercent = 5
max(EnLimPercent) = 100

CONST EnLimStat = 6' to 17
FOR i as integer = 0 TO 1:  max(EnLimStat + i) = 32767: NEXT i ' HP and MP
FOR i as integer = 2 TO 8:  max(EnLimStat + i) = 999:   NEXT i ' regular stats
FOR i as integer = 9 TO 10: max(EnLimStat + i) = 100:   NEXT i ' focus, counter
max(EnLimStat + 11) = 10        ' max hits

CONST EnLimSpawn = 18
max(EnLimSpawn) = gen(genMaxEnemy) + 1 'must be updated!

CONST EnLimSpawnNum = 19
max(EnLimSpawnNum) = 8

CONST EnLimAtk = 20
max(EnLimAtk) = gen(genMaxAttack) + 1

CONST EnLimStr16 = 21
max(EnLimStr16) = 16

CONST EnLimStealAvail = 22
min(EnLimStealAvail) = -1
max(EnLimStealAvail) = 1
addcaption caption(), capindex, "Disabled"
DIM EnCapStealAvail as integer = capindex
addcaption caption(), capindex, "Only one"
addcaption caption(), capindex, "Unlimited"

CONST EnLimPal16 = 23
max(EnLimPal16) = 32767
min(EnLimPal16) = -1

CONST EnLimDissolve = 24
min(EnLimDissolve) = 0
max(EnLimDissolve) = dissolveTypeMax + 1
DIM EnCapDissolve as integer = capindex
addcaption caption(), capindex, "Global Default"
FOR i as integer = 0 TO dissolveTypeMax
 addcaption caption(), capindex, dissolve_type_caption(i)
NEXT

CONST EnLimDissolveTime = 25
min(EnLimDissolveTime) = 0
max(EnLimDissolveTime) = 99

CONST EnLimDeathSFX = 26
min(EnLimDeathSFX) = -1
max(EnLimDeathSFX) = gen(genMaxSFX) + 1

DIM EnCapElemResist as integer = capindex
FOR i as integer = 0 TO gen(genNumElements) - 1
 addcaption caption(), capindex, ""  '--updated in update_enemy_editor_for_elementals
NEXT

'--next limit 27, remeber to update dim!

'-------------------------------------------------------------------------
'--menu content
DIM menu(260) as string
DIM menutype(260) as integer
DIM menuoff(260) as integer
DIM menulimits(260) as integer

CONST EnMenuBackAct = 0
menu(EnMenuBackAct) = "Previous Menu"
menutype(EnMenuBackAct) = 1

CONST EnMenuChooseAct = 1
menu(EnMenuChooseAct) = "Enemy"
menutype(EnMenuChooseAct) = 5

CONST EnMenuName = 2
menu(EnMenuName) = "Name:"
menutype(EnMenuName) = 4
menuoff(EnMenuName) = EnDatName
menulimits(EnMenuName) = EnLimStr16

CONST EnMenuAppearAct = 3
menu(EnMenuAppearAct) = "Appearance & Sounds..."
menutype(EnMenuAppearAct) = 1

CONST EnMenuRewardAct = 4
menu(EnMenuRewardAct) = "Rewards..."
menutype(EnMenuRewardAct) = 1

CONST EnMenuStatAct = 5
menu(EnMenuStatAct) = "Stats..."
menutype(EnMenuStatAct) = 1

CONST EnMenuBitsetAct = 6
menu(EnMenuBitsetAct) = "Bitsets..."
menutype(EnMenuBitsetAct) = 1

CONST EnMenuSpawnAct = 7
menu(EnMenuSpawnAct) = "Spawning..."
menutype(EnMenuSpawnAct) = 1

CONST EnMenuAtkAct = 8
menu(EnMenuAtkAct) = "Attacks..."
menutype(EnMenuAtkAct) = 1

CONST EnMenuPic = 9
menu(EnMenuPic) = "Picture:"
menutype(EnMenuPic) = 0
menuoff(EnMenuPic) = EnDatPic
menulimits(EnMenuPic) = EnLimPic

CONST EnMenuPal = 10
menu(EnMenuPal) = "Palette:"
menutype(EnMenuPal) = 12
menuoff(EnMenuPal) = EnDatPal
menulimits(EnMenuPal) = EnLimPal16

CONST EnMenuPicSize = 11
menu(EnMenuPicSize) = "Picture Size:"
menutype(EnMenuPicSize) = 2000 + EnCapPicSize
menuoff(EnMenuPicSize) = EnDatPicSize
menulimits(EnMenuPicSize) = EnLimPicSize

CONST EnMenuGold = 12
menu(EnMenuGold) = "Gold:"
menutype(EnMenuGold) = 0
menuoff(EnMenuGold) = EnDatGold
menulimits(EnMenuGold) = EnLimUInt

CONST EnMenuExp = 13
menu(EnMenuExp) = "Experience Points:"
menutype(EnMenuExp) = 0
menuoff(EnMenuExp) = EnDatExp
menulimits(EnMenuExp) = EnLimUInt

CONST EnMenuItem = 14
menu(EnMenuItem) = "Item:"
menutype(EnMenuItem) = 8
menuoff(EnMenuItem) = EnDatItem
menulimits(EnMenuItem) = EnLimItem

CONST EnMenuItemP = 15
menu(EnMenuItemP) = "Item%:"
menutype(EnMenuItemP) = 0
menuoff(EnMenuItemP) = EnDatItemP
menulimits(EnMenuItemP) = EnLimPercent

CONST EnMenuRareItem = 16
menu(EnMenuRareItem) = "Rare Item:"
menutype(EnMenuRareItem) = 8
menuoff(EnMenuRareItem) = EnDatRareItem
menulimits(EnMenuRareItem) = EnLimItem

CONST EnMenuRareItemP = 17
menu(EnMenuRareItemP) = "Rare Item%:"
menutype(EnMenuRareItemP) = 0
menuoff(EnMenuRareItemP) = EnDatRareItemP
menulimits(EnMenuRareItemP) = EnLimPercent

CONST EnMenuStat = 18' to 29
FOR i as integer = 0 TO 11
 menu(EnMenuStat + i) = statnames(i) + ":"
 menutype(EnMenuStat + i) = 0
 menuoff(EnMenuStat + i) = EnDatStat + i
 menulimits(EnMenuStat + i) = EnLimStat + i
NEXT i
menutype(EnMenuStat + 8) = 15 'Speed should show turn-time estimate

CONST EnMenuSpawnDeath = 30
menu(EnMenuSpawnDeath) = "Spawn on Death:"
menutype(EnMenuSpawnDeath) = 9
menuoff(EnMenuSpawnDeath) = EnDatSpawnDeath
menulimits(EnMenuSpawnDeath) = EnLimSpawn

CONST EnMenuSpawnNEDeath = 31
menu(EnMenuSpawnNEDeath) = "on Non-Elemental Death:"
menutype(EnMenuSpawnNEDeath) = 9
menuoff(EnMenuSpawnNEDeath) = EnDatSpawnNEDeath
menulimits(EnMenuSpawnNEDeath) = EnLimSpawn

CONST EnMenuSpawnAlone = 32
menu(EnMenuSpawnAlone) = "Spawn When Alone:"
menutype(EnMenuSpawnAlone) = 9
menuoff(EnMenuSpawnAlone) = EnDatSpawnAlone
menulimits(EnMenuSpawnAlone) = EnLimSpawn

CONST EnMenuSpawnNEHit = 33
menu(EnMenuSpawnNEHit) = "on Non-Elemental Hit:"
menutype(EnMenuSpawnNEHit) = 9
menuoff(EnMenuSpawnNEHit) = EnDatSpawnNEHit
menulimits(EnMenuSpawnNEHit) = EnLimSpawn

CONST EnMenuSpawnElement = 34' to 93
FOR i as integer = 0 TO gen(genNumElements) - 1
 menu(EnMenuSpawnElement + i) = "on " & elementnames(i) & " Hit:"
 menutype(EnMenuSpawnElement + i) = 9
 IF i < 8 THEN
  menuoff(EnMenuSpawnElement + i) = EnDatSpawnElement + i
 ELSE
  menuoff(EnMenuSpawnElement + i) = EnDatSpawnElement2 + (i - 8)
 END IF
 menulimits(EnMenuSpawnElement + i) = EnLimSpawn
NEXT i

CONST EnMenuSpawnNum = 94
menu(EnMenuSpawnNum) = "How Many to Spawn:"
menutype(EnMenuSpawnNum) = 0
menuoff(EnMenuSpawnNum) = EnDatSpawnNum
menulimits(EnMenuSpawnNum) = EnLimSpawnNum

CONST EnMenuAtkNormal = 95' to 99
FOR i as integer = 0 TO 4
 menu(EnMenuAtkNormal + i) = "Normal:"
 menutype(EnMenuAtkNormal + i) = 7
 menuoff(EnMenuAtkNormal + i) = EnDatAtkNormal + i
 menulimits(EnMenuAtkNormal + i) = EnLimAtk
NEXT i

CONST EnMenuAtkDesp = 100' to 104
FOR i as integer = 0 TO 4
 menu(EnMenuAtkDesp + i) = "Desperation:"
 menutype(EnMenuAtkDesp + i) = 7
 menuoff(EnMenuAtkDesp + i) = EnDatAtkDesp + i
 menulimits(EnMenuAtkDesp + i) = EnLimAtk
NEXT i

CONST EnMenuAtkAlone = 105' to 109
FOR i as integer = 0 TO 4
 menu(EnMenuAtkAlone + i) = "Alone:"
 menutype(EnMenuAtkAlone + i) = 7
 menuoff(EnMenuAtkAlone + i) = EnDatAtkAlone + i
 menulimits(EnMenuAtkAlone + i) = EnLimAtk
NEXT i

CONST EnMenuStealItem = 110
menu(EnMenuStealItem) = "Stealable Item:"
menutype(EnMenuStealItem) = 8
menuoff(EnMenuStealItem) = EnDatStealItem
menulimits(EnMenuStealItem) = EnLimItem

CONST EnMenuStealRItem = 111
menu(EnMenuStealRItem) = "Rare Stealable Item:"
menutype(EnMenuStealRItem) = 8
menuoff(EnMenuStealRItem) = EnDatStealRItem
menulimits(EnMenuStealRItem) = EnLimItem

CONST EnMenuStealItemP = 112
menu(EnMenuStealItemP) = "Steal Rate%:"
menutype(EnMenuStealItemP) = 0
menuoff(EnMenuStealItemP) = EnDatStealItemP
menulimits(EnMenuStealItemP) = EnLimPercent

CONST EnMenuStealRItemP = 113
menu(EnMenuStealRItemP) = "Rare Steal Rate%:"
menutype(EnMenuStealRItemP) = 0
menuoff(EnMenuStealRItemP) = EnDatStealRItemP
menulimits(EnMenuStealRItemP) = EnLimPercent

CONST EnMenuStealAvail = 114
menu(EnMenuStealAvail) = "Steal Availability:"
menutype(EnMenuStealAvail) = 2000 + EnCapStealAvail
menuoff(EnMenuStealAvail) = EnDatStealAvail
menulimits(EnMenuStealAvail) = EnLimStealAvail

CONST EnMenuDissolve = 115
menu(EnMenuDissolve) = "Death Animation:"
menutype(EnMenuDissolve) = 2000 + EnCapDissolve
menuoff(EnMenuDissolve) = EnDatDissolve
menulimits(EnMenuDissolve) = EnLimDissolve

CONST EnMenuDissolveTime = 116
menu(EnMenuDissolveTime) = "Death Animation ticks:"
menutype(EnMenuDissolveTime) = 13
menuoff(EnMenuDissolveTime) = EnDatDissolveTime
menulimits(EnMenuDissolveTime) = EnLimDissolveTime

CONST EnMenuDeathSFX = 117
menu(EnMenuDeathSFX) = "Death Sound Effect:"
menutype(EnMenuDeathSFX) = 14
menuoff(EnMenuDeathSFX) = EnDatDeathSFX
menulimits(EnMenuDeathSFX) = EnLimDeathSFX

CONST EnMenuCursorOffset = 118
menu(EnMenuCursorOffset) = "Cursor Offset..."
menutype(EnMenuCursorOffset) = 1

CONST EnMenuElemCtr = 119' to 182
FOR i as integer = 0 TO gen(genNumElements) - 1
 menu(EnMenuElemCtr + i) = "Counter element " & elementnames(i) & ":"
 menutype(EnMenuElemCtr + i) = 7
 IF i < 8 THEN
  menuoff(EnMenuElemCtr + i) = EnDatElemCtr + i
 ELSE
  menuoff(EnMenuElemCtr + i) = EnDatElemCtr2 + (i - 8)
 END IF
 menulimits(EnMenuElemCtr + i) = EnLimAtk
NEXT i

CONST EnMenuStatCtr = 183' to 194
FOR i as integer = 0 TO 11
 menu(EnMenuStatCtr + i) = "Counter damage to " & statnames(i) & ":"
 menutype(EnMenuStatCtr + i) = 7
 menuoff(EnMenuStatCtr + i) = EnDatStatCtr + i
 menulimits(EnMenuStatCtr + i) = EnLimAtk
NEXT i

CONST EnMenuElementalsAct = 195
menu(EnMenuElementalsAct) = "Elemental Resistances..."
menutype(EnMenuElementalsAct) = 1

CONST EnMenuElemDmg = 196' to 259
FOR i as integer = 0 TO gen(genNumElements) - 1
 menu(EnMenuElemDmg + i) = "Damage from " + rpad(elementnames(i), " ", 15) + ":"
 menutype(EnMenuElemDmg + i) = 5000 + EnCapElemResist + i  'percent_grabber
 menuoff(EnMenuElemDmg + i) = 239 + i*2 
NEXT

CONST EnMenuAtkBequest = 260
menu(EnMenuAtkBequest) = "On-Death Bequest Attack:"
menutype(EnMenuAtkBequest) = 7
menuoff(EnMenuAtkBequest) = EnDatAtkBequest
menulimits(EnMenuAtkBequest) = EnLimAtk

'-------------------------------------------------------------------------
'--menu structure
'WARNING: make these big enough to hold atkMenu when genNumElements is maxed out
DIM workmenu(92) as integer
DIM dispmenu(92) as string
DIM state as MenuState
state.size = 24
state.need_update = YES

DIM mainMenu(9) as integer
mainMenu(0) = EnMenuBackAct
mainMenu(1) = EnMenuChooseAct
mainMenu(2) = EnMenuName
mainMenu(3) = EnMenuAppearAct
mainMenu(4) = EnMenuRewardAct
mainMenu(5) = EnMenuStatAct
mainMenu(6) = EnMenuBitsetAct
mainMenu(7) = EnMenuElementalsAct
mainMenu(8) = EnMenuSpawnAct
mainMenu(9) = EnMenuAtkAct

DIM appearMenu(7) as integer
appearMenu(0) = EnMenuBackAct
appearMenu(1) = EnMenuPicSize
appearMenu(2) = EnMenuPic
appearMenu(3) = EnMenuPal
appearMenu(4) = EnMenuDissolve
appearMenu(5) = EnMenuDissolveTime
appearMenu(6) = EnMenuDeathSFX
appearMenu(7) = EnMenuCursorOffset

DIM rewardMenu(11) as integer
rewardMenu(0) = EnMenuBackAct
rewardMenu(1) = EnMenuGold
rewardMenu(2) = EnMenuExp
rewardMenu(3) = EnMenuItem
rewardMenu(4) = EnMenuItemP
rewardMenu(5) = EnMenuRareItem
rewardMenu(6) = EnMenuRareItemP
rewardMenu(7) = EnMenuStealAvail
rewardMenu(8) = EnMenuStealItem
rewardMenu(9) = EnMenuStealItemP
rewardMenu(10) = EnMenuStealRItem
rewardMenu(11) = EnMenuStealRItemP

DIM statMenu(12) as integer
statMenu(0) = EnMenuBackAct
FOR i as integer = 0 TO 11
 statMenu(1 + i) = EnMenuStat + i
NEXT i

DIM spawnMenu(5 + gen(genNumElements)) as integer
spawnMenu(0) = EnMenuBackAct
spawnMenu(1) = EnMenuSpawnNum
spawnMenu(2) = EnMenuSpawnDeath
spawnMenu(3) = EnMenuSpawnNEDeath
spawnMenu(4) = EnMenuSpawnAlone
spawnMenu(5) = EnMenuSpawnNEHit
FOR i as integer = 0 TO gen(genNumElements) - 1
 spawnMenu(6 + i) = EnMenuSpawnElement + i
NEXT i

DIM atkMenu(28 + gen(genNumElements)) as integer
atkMenu(0) = EnMenuBackAct
FOR i as integer = 0 TO 4
 atkMenu(1 + i) = EnMenuAtkNormal + i
 atkMenu(6 + i) = EnMenuAtkDesp + i
 atkMenu(11 + i) = EnMenuAtkAlone + i
NEXT i
atkMenu(16) = EnMenuAtkBequest
FOR i as integer = 0 TO gen(genNumElements) - 1
 atkMenu(17 + i) = EnMenuElemCtr + i
NEXT i
FOR i as integer = 0 TO 11
 atkMenu(17 + gen(genNumElements) + i) = EnMenuStatCtr + i
NEXT i

DIM elementalMenu(gen(genNumElements)) as integer
elementalMenu(0) = EnMenuBackAct
FOR i as integer = 0 TO gen(genNumElements) - 1
 elementalMenu(1 + i) = EnMenuElemDmg + i
NEXT i

DIM helpkey as string = "enemy"

'--Create the box that holds the preview
DIM preview_box as Slice Ptr
preview_box = NewSliceOfType(slRectangle)
ChangeRectangleSlice preview_box, ,uilook(uiDisabledItem), uilook(uiMenuItem), , transOpaque
'--Align the box in the bottom right
WITH *preview_box
 .X = -8
 .Y = -8
 .Width = 82
 .Height = 82
 .AnchorHoriz = 2
 .AlignHoriz = 2
 .AnchorVert = 2
 .AlignVert = 2
END WITH

'--Create the preview sprite. It will be updated before it is drawn.
DIM preview as Slice Ptr
preview = NewSliceOfType(slSprite, preview_box)
'--Align the sprite to the bottom center of the containing box
WITH *preview
 .Y = -1
 .AnchorHoriz = 1
 .AlignHoriz = 1
 .AnchorVert = 2
 .AlignVert = 2
END WITH

'--Need a copy of the sprite to call frame_dissolved on
DIM preview_sprite as Frame ptr

'--dissolve_ticks is >= 0 while playing a dissolve; > dissolve_time while during lag period afterwards
DIM as integer dissolve_time, dissolve_type, dissolve_ticks
dissolve_ticks = -1

'--default starting menu
setactivemenu workmenu(), mainMenu(), state

DIM menudepth as bool = NO
DIM lastptr as integer = 0
DIM lasttop as integer = 0
DIM recindex as integer = 0

DIM rememberindex as integer = -1
DIM remember_bit as integer = -1
DIM show_name as integer = 0
DIM drawpreview as integer = YES

'load data here
enemy_edit_load recindex, recbuf(), state, caption(), EnCapElemResist

'------------------------------------------------------------------------
'--main loop

setkeys YES
DO
 setwait 55
 setkeys YES
 IF keyval(scESC) > 1 THEN
  IF menudepth THEN
   enemy_edit_backmenu state, lastptr, lasttop, menudepth, workmenu(), mainMenu()
   helpkey = "enemy"
   drawpreview = YES
  ELSE
   EXIT DO
  END IF
 END IF

 '--CTRL+BACKSPACE
 IF keyval(scCtrl) > 0 AND keyval(scBackspace) > 0 THEN
  cropafter recindex, gen(genMaxEnemy), 0, game + ".dt1", getbinsize(binDT1)
 END IF

 usemenu state

 IF workmenu(state.pt) = EnMenuChooseAct OR (keyval(scAlt) > 0 and NOT isStringField(menutype(workmenu(state.pt)))) THEN
  DIM lastindex as integer = recindex
  IF intgrabber_with_addset(recindex, 0, gen(genMaxEnemy), 32767, "enemy") THEN
   saveenemydata recbuf(), lastindex
   IF recindex > gen(genMaxEnemy) THEN
    '--adding a new set
    IF enemy_edit_add_new(recbuf(), recindex, preview_box) THEN
     saveenemydata recbuf(), recindex
    ELSE
     enemy_edit_load recindex, recbuf(), state, caption(), EnCapElemResist
    END IF
    update_enemy_editor_for_elementals recbuf(), caption(), EnCapElemResist
    state.need_update = YES
   ELSE
    enemy_edit_load recindex, recbuf(), state, caption(), EnCapElemResist
   END IF
  END IF
 END IF

 IF keyval(scF1) > 1 THEN show_help helpkey

 IF keyval(scTab) > 1 THEN
  IF keyval(scLeftShift) > 0 OR keyval(scRightShift) > 0 THEN
   rememberindex = recindex
  ELSEIF rememberindex >= 0 AND rememberindex <= gen(genMaxEnemy) THEN
   saveenemydata recbuf(), recindex
   SWAP rememberindex, recindex
   enemy_edit_load recindex, recbuf(), state, caption(), EnCapElemResist
   show_name = 23
  END IF
 END IF

 IF enter_space_click(state) THEN
  SELECT CASE workmenu(state.pt)
   CASE EnMenuBackAct
    IF menudepth THEN
     enemy_edit_backmenu state, lastptr, lasttop, menudepth, workmenu(), mainMenu()
     helpkey = "enemy"
     drawpreview = YES
    ELSE
     EXIT DO
    END IF
   CASE EnMenuAppearAct
    enemy_edit_pushmenu state, lastptr, lasttop, menudepth
    setactivemenu workmenu(), appearMenu(), state
    helpkey = "enemy_appearance"
    state.need_update = YES
   CASE EnMenuRewardAct
    enemy_edit_pushmenu state, lastptr, lasttop, menudepth
    setactivemenu workmenu(), rewardMenu(), state
    helpkey = "enemy_rewards"
    state.need_update = YES
   CASE EnMenuStatAct
    enemy_edit_pushmenu state, lastptr, lasttop, menudepth
    setactivemenu workmenu(), statMenu(), state
    helpkey = "enemy_stats"
    state.need_update = YES
   CASE EnMenuSpawnAct
    enemy_edit_pushmenu state, lastptr, lasttop, menudepth
    setactivemenu workmenu(), spawnMenu(), state
    helpkey = "enemy_spawning"
    drawpreview = NO
    state.need_update = YES
   CASE EnMenuAtkAct
    enemy_edit_pushmenu state, lastptr, lasttop, menudepth
    setactivemenu workmenu(), atkMenu(), state
    helpkey = "enemy_attacks"
    drawpreview = NO
    state.need_update = YES
   CASE EnMenuElementalsAct
    enemy_edit_pushmenu state, lastptr, lasttop, menudepth
    setactivemenu workmenu(), elementalMenu(), state
    helpkey = "enemy_elementals"
    drawpreview = NO
    state.need_update = YES
   CASE EnMenuPal
    recbuf(EnDatPal) = pal16browse(recbuf(EnDatPal), recbuf(EnDatPicSize) + 1, recbuf(EnDatPic))
    state.need_update = YES
   CASE EnMenuDeathSFX
    IF recbuf(EnDatDeathSFX) >= 1 THEN playsfx recbuf(EnDatDeathSFX) - 1
    IF recbuf(EnDatDeathSFX) = 0 THEN playsfx gen(genDefaultDeathSFX) - 1
   CASE EnMenuBitsetAct
    editbitset recbuf(), EnDatBitset, UBOUND(ebit), ebit(), "enemy_bitsets", remember_bit
   CASE EnMenuDissolve, EnMenuDissolveTime
    IF recbuf(EnDatDissolve) THEN dissolve_type = recbuf(EnDatDissolve) - 1 ELSE dissolve_type = gen(genEnemyDissolve)
    dissolve_time = recbuf(EnDatDissolveTime) 
    IF dissolve_time = 0 THEN dissolve_time = default_dissolve_time(dissolve_type, preview_sprite->w, preview_sprite->h)
    dissolve_ticks = 0
   CASE EnMenuCursorOffset
    '--temporarily move the preview image
    SetSliceParent(preview, SliceTable.Root)
    preview->AnchorVert = 1
    preview->AlignVert = 1
    WITH sprite_sizes(recbuf(EnDatPicSize) + 1)
     recbuf(EnDatCursorX) += .size.x / 2 '--offset relative to the top middle
     xy_position_on_slice preview, recbuf(EnDatCursorX), recbuf(EnDatCursorY), "Targetting Cursor Offset", "xy_target_cursor"
     recbuf(EnDatCursorX) -= .size.x / 2
    END WITH
    '--move the preview image back how it was before
    SetSliceParent(preview, preview_box)
    preview->AnchorVert = 2
    preview->AlignVert = 2
  END SELECT
 END IF

 IF keyval(scAlt) = 0 or isStringField(menutype(workmenu(state.pt))) THEN 'not pressing ALT, or not allowed to
  IF editflexmenu(workmenu(state.pt), menutype(), menuoff(), menulimits(), recbuf(), caption(), min(), max()) THEN
   state.need_update = YES
  END IF
 END IF

 IF dissolve_ticks >= 0 THEN
  dissolve_ticks += 1
  IF dissolve_ticks > dissolve_time + 15 THEN
   dissolve_ticks = -1
   state.need_update = YES
  ELSE
   IF dissolve_ticks <= dissolve_time THEN
    SetSpriteToFrame preview, frame_dissolved(preview_sprite, dissolve_time, dissolve_ticks, dissolve_type), _
                     abs_pal_num(recbuf(EnDatPal), 1 + recbuf(EnDatPicSize), recbuf(EnDatPic))
   END IF
  END IF
 END IF
 'lag time after fading out, to give a more realistic preview
 preview->Visible = (dissolve_ticks <= dissolve_time)

 IF state.need_update THEN
  state.need_update = NO
  enemy_edit_update_menu recindex, state, recbuf(), menu(), menuoff(), menutype(), menulimits(), min(), max(), dispmenu(), workmenu(), caption(), preview_sprite, preview, dissolve_ticks, EnLimSpawn, EnLimPic, EnDatPic, EnDatPal, EnDatPicSize
 END IF

 clearpage vpage
 IF drawpreview THEN
  DrawSlice preview_box, vpage
 END IF

 standardmenu dispmenu(), state, 0, 0, vpage
 draw_fullscreen_scrollbar state, , vpage
 IF keyval(scAlt) > 0 OR show_name > 0 THEN 'holding ALT or just pressed TAB
  show_name = large(0, show_name - 1)
  DIM tmpstr as string = readbadbinstring(recbuf(), EnDatName, 15, 0) & " " & recindex
  textcolor uilook(uiText), uilook(uiHighlight)
  printstr tmpstr, 320 - LEN(tmpstr) * 8, 0, vpage
 END IF

 setvispage vpage
 dowait
LOOP

'--save what we were last working on
saveenemydata recbuf(), recindex

resetsfx
clearallpages
DeleteSlice @preview_box
frame_unload @preview_sprite

END SUB

SUB enemy_edit_backmenu (state as MenuState, byval lastptr as integer, byval lasttop as integer, byref menudepth as bool, workmenu() as integer, mainMenu() as integer)
 setactivemenu workmenu(), mainMenu(), state
 menudepth = NO
 state.pt = lastptr
 state.top = lasttop
 state.need_update = YES
END SUB

SUB enemy_edit_pushmenu (state as MenuState, byref lastptr as integer, byref lasttop as integer, byref menudepth as bool)
 lastptr = state.pt
 lasttop = state.top
 menudepth = YES
END SUB

SUB enemy_edit_load(byval recnum as integer, recbuf() as integer, state as MenuState, caption() as string, byval EnCapElemResist as integer)
 loadenemydata recbuf(), recnum
 update_enemy_editor_for_elementals recbuf(), caption(), EnCapElemResist
 state.need_update = YES
END SUB

SUB enemy_edit_update_menu(byval recindex as integer, state as MenuState, recbuf() as integer, menu() as string, menuoff() as integer, menutype() as integer, menulimits() as integer, min() as integer, max() as integer, dispmenu() as string, workmenu() as integer, caption() as string, byref preview_sprite as Frame ptr, preview as Slice Ptr, byval dissolve_ticks as integer, byval EnLimSpawn as integer, byval EnLimPic as integer, byval EnDatPic as integer, byval EnDatPal as integer, byval EnDatPicSize as integer)

 '--in case new enemies have been added
 max(EnLimSpawn) = gen(genMaxEnemy) + 1
 
 '--in case the PicSize has changed
 max(EnLimPic) = gen(genMaxEnemy1Pic + bound(recbuf(EnDatPicSize), 0, 2))
 
 '--re-enforce bounds, as they might have just changed
 enforceflexbounds menuoff(), menutype(), menulimits(), recbuf(), min(), max()
 
 updateflexmenu state.pt, dispmenu(), workmenu(), state.last, menu(), menutype(), menuoff(), menulimits(), recbuf(), caption(), max(), recindex
 
 '--stop sounds
 resetsfx
 '--update the picture and palette preview
 frame_unload @preview_sprite
 preview_sprite = frame_load(1 + recbuf(EnDatPicSize), recbuf(EnDatPic))
 dissolve_ticks = -1
 '--resets if dissolved
 ChangeSpriteSlice preview, 1 + recbuf(EnDatPicSize), recbuf(EnDatPic), recbuf(EnDatPal), ,YES

END SUB

FUNCTION enemy_edit_add_new (recbuf() as integer, byref recindex as integer, preview_box as Slice Ptr) as integer
 'Returns YES if a new record was added, or NO if cancelled.
 'The recbuf() will be populated with the new blank or cloned record
  DIM enemy as EnemyDef
  DIM menu(2) as string
  DIM enemytocopy as integer = 0
  DIM preview as Slice ptr = preview_box->FirstChild
  DIM state as MenuState
  state.last = UBOUND(menu)
  state.size = 24
  state.pt = 1

  state.need_update = YES
  setkeys
  DO
    setwait 55
    setkeys
    IF keyval(scESC) > 1 THEN  'cancel
      recindex -= 1
      RETURN NO
    END IF
    IF keyval(scF1) > 1 THEN show_help "enemy_new"
    usemenu state
    IF state.pt = 2 THEN
      IF intgrabber(enemytocopy, 0, gen(genMaxEnemy)) THEN state.need_update = YES
    END IF
    IF state.need_update THEN
      state.need_update = NO
      loadenemydata recbuf(), enemytocopy
      loadenemydata enemy, enemytocopy
      ChangeSpriteSlice preview, 1 + enemy.size, enemy.pic, enemy.pal, , YES

      menu(0) = "Cancel"
      menu(1) = "New Blank Enemy"
      menu(2) = "Copy of Enemy " & enemytocopy & " " & enemy.name
    END IF
    IF enter_space_click(state) THEN
      SELECT CASE state.pt
        CASE 0 ' cancel
          recindex -= 1
          RETURN NO
        CASE 1 ' blank
          gen(genMaxEnemy) = recindex
          clearenemydata recbuf()
          RETURN YES
        CASE 2 ' copy
          gen(genMaxEnemy) = recindex
          RETURN YES
      END SELECT
    END IF

    clearpage vpage
    standardmenu menu(), state, 0, 0, vpage
    IF state.pt = 2 THEN DrawSlice preview_box, vpage
    setvispage vpage
    dowait
  LOOP
END FUNCTION


'--------------------------------- Formation Editor ----------------------------


SUB formation_editor
 DIM menu(2) as string
 menu(0) = "Return to Main Menu"
 menu(1) = "Edit Individual Formations..."
 menu(2) = "Construct Formation Sets..."

 DIM state as MenuState
 state.size = 24
 state.last = UBOUND(menu)

 setkeys
 DO
  setwait 55
  setkeys
  IF keyval(scESC) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "formation_main"
  usemenu state
  IF enter_space_click(state) THEN
   IF state.pt = 0 THEN EXIT DO
   IF state.pt = 1 THEN individual_formation_editor
   IF state.pt = 2 THEN formation_set_editor
  END IF

  clearpage dpage
  standardmenu menu(), state, 0, 0, dpage

  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
END SUB

SUB formation_set_editor
 DIM form as Formation
 DIM formset as FormationSet
 DIM set_id as integer = 1, form_id as integer
 DIM menu(23) as string
 DIM rootslice as Slice ptr
 DIM as string ename(7)
 DIM state as MenuState
 state.last = UBOUND(menu)
 state.size = 24
 DIM menuopts as MenuOptions
 menuopts.edged = YES

 LoadFormationSet formset, set_id
 GOSUB lpreviewform
 setkeys
 DO
  setwait 55
  setkeys
  IF keyval(scESC) > 1 THEN
   SaveFormationSet formset, set_id
   EXIT DO
  END IF
  IF keyval(scF1) > 1 THEN show_help "formation_sets"
  IF usemenu(state) THEN GOSUB lpreviewform
  IF enter_space_click(state) THEN
   IF state.pt = 0 THEN
    SaveFormationSet formset, set_id
    EXIT DO
   END IF
  END IF
  IF state.pt = 1 THEN
   DIM remember_id as integer = set_id
   IF intgrabber(set_id, 1, 255) THEN
    SaveFormationSet formset, remember_id
    LoadFormationSet formset, set_id
   END IF
  END IF
  IF state.pt = 2 THEN intgrabber formset.frequency, 0, 200
  IF state.pt = 3 THEN tag_grabber formset.tag
  IF state.pt >= 4 THEN
   IF intgrabber(formset.formations(state.pt - 4), -1, gen(genMaxFormation)) THEN
    GOSUB lpreviewform
   END IF
  END IF
  IF state.pt >= 4 AND form_id >= 0 THEN
   draw_formation_slices form, rootslice, -1, dpage
  ELSE
   clearpage dpage
  END IF
  menu(0) = "Previous Menu"
  menu(1) = CHR(27) & "Formation Set " & set_id & CHR(26)
  menu(2) = "Battle Frequency: " & formset.frequency & " (" & step_estimate(formset.frequency, 60, 100, "-", " steps") & ")"
  menu(3) = tag_condition_caption(formset.tag, "Only if tag", "No tag check")
  FOR i as integer = 0 TO 19
   IF formset.formations(i) = -1 THEN
    menu(4 + i) = "Empty"
   ELSE
    menu(4 + i) = "Formation " & formset.formations(i)
   END IF
  NEXT i

  standardmenu menu(), state, 0, 0, dpage, menuopts

  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
 DeleteSlice @rootslice
 EXIT SUB

lpreviewform:
 IF state.pt >= 4 THEN
  '--have form selected
  form_id = formset.formations(state.pt - 4)
  IF form_id >= 0 THEN
   '--form not empty
   LoadFormation form, form_id
   load_formation_slices ename(), form, @rootslice
  END IF
 END IF
 RETRACE

END SUB

SUB individual_formation_editor ()
 DIM form_id as integer = 0
 DIM form as Formation
 DIM ename(7) as string
 DIM rootslice as Slice ptr
 DIM as integer i
 DIM positioning_mode as bool = NO
 DIM as integer bgwait, bgctr

 LoadFormation form, form_id
 load_formation_slices ename(), form, @rootslice

 DIM menu(13) as string
 DIM state as MenuState
 state.pt = 0
 state.top = 0
 state.first = 0
 state.last = UBOUND(menu)
 state.size = 20
 DIM menuopts as MenuOptions
 menuopts.edged = YES

 DIM slot as integer = state.pt - 6

 setkeys
 DO
  setwait 55
  setkeys
  IF positioning_mode = YES THEN
   '--enemy positioning mode
   IF keyval(scESC) > 1 OR enter_or_space() THEN setkeys: positioning_mode = NO
   IF keyval(scF1) > 1 THEN show_help "formation_editor_placement"
   DIM as integer thiswidth = 0, thisheight = 0, movespeed = 1
   IF keyval(scLeftShift) OR keyval(scRightShift) THEN movespeed = 8
   WITH form.slots(slot)
    DIM sprite as Slice ptr = LookupSlice(SL_FORMEDITOR_ENEMY + slot, rootslice)
    IF sprite THEN
     thiswidth = sprite->Width
     thisheight = sprite->Height
    END IF
    ' Note that enemy positions are the top-left corner of the sprite
    ' (which needs to be changed)
    IF keyval(scUp) > 0 THEN .pos.y -= movespeed
    IF keyval(scDown) > 0 THEN .pos.y += movespeed
    IF keyval(scLeft) > 0 THEN .pos.x -= movespeed
    IF keyval(scRight) > 0 THEN .pos.x += movespeed
    .pos.x = bound(.pos.x, -thiswidth\2, gen(genResolutionX) - thiswidth\2)
    .pos.y = bound(.pos.y, -thisheight\2, gen(genResolutionY) - thisheight\2)
   END WITH
  END IF
  IF positioning_mode = NO THEN
   '--menu mode
   IF keyval(scESC) > 1 THEN
    EXIT DO
   END IF
   IF keyval(scF1) > 1 THEN show_help "formation_editor"
   IF keyval(scCtrl) > 0 AND keyval(scBackspace) > 0 THEN cropafter form_id, gen(genMaxFormation), 0, game + ".for", 80
   usemenu state
   slot = state.pt - 6

   IF enter_space_click(state) THEN
    IF state.pt = 0 THEN
     EXIT DO
    END IF
    IF state.pt = 5 THEN
     IF form.music >= 0 THEN playsongnum form.music
    END IF
    IF state.pt >= 6 THEN 'an enemy
     IF form.slots(slot).id >= 0 THEN positioning_mode = YES
    END IF
   END IF
   IF state.pt = 2 THEN
    IF intgrabber(form.background, 0, gen(genNumBackdrops) - 1) THEN
     bgwait = 0
     bgctr = 0
     load_formation_slices ename(), form, @rootslice
    END IF
   END IF
   IF state.pt = 3 THEN
    'IF intgrabber(form.background_frames, 1, 50) THEN
    DIM temp as integer = form.background_frames - 1
    IF xintgrabber(temp, 2, 50) THEN
     IF form.background_frames = 1 THEN form.background_ticks = 8  'default to 8 ticks because 1 tick can be really painful
     form.background_frames = temp + 1
     IF bgctr >= form.background_frames THEN
      bgctr = 0
      load_formation_slices ename(), form, @rootslice
     END IF
    END IF
   END IF
   IF state.pt = 4 THEN
    IF intgrabber(form.background_ticks, 0, 1000) THEN
     bgwait = 0
    END IF
   END IF
   IF state.pt = 5 THEN
    IF intgrabber(form.music, -2, gen(genMaxSong)) THEN
     music_stop
    END IF
   END IF
   IF state.pt = 1 THEN '---SELECT A DIFFERENT FORMATION
    DIM as integer remember_id = form_id
    IF intgrabber_with_addset(form_id, 0, gen(genMaxFormation), 32767, "formation") THEN
     SaveFormation form, remember_id
     IF form_id > gen(genMaxFormation) THEN
      gen(genMaxFormation) = form_id
      ClearFormation form
      form.music = gen(genBatMus) - 1
      SaveFormation form, form_id
     END IF
     LoadFormation form, form_id
     load_formation_slices ename(), form, @rootslice
     bgwait = 0
     bgctr = 0
    END IF
   END IF'--DONE SELECTING DIFFERENT FORMATION
   IF state.pt >= 6 THEN
    WITH form.slots(slot)
     DIM oldenemy as integer = .id
     IF intgrabber(.id, -1, gen(genMaxEnemy)) THEN
      'This would treat the x/y position as being the bottom middle of enemies, which makes much more
      'sense, but that would change where enemies of different sizes are spawned in slots in existing games
      'See the Plan for battle formation improvements
      '.pos.x += w(slot) \ 2
      '.pos.y += h(slot)
      load_formation_slices ename(), form, @rootslice
      'default to middle of field
      IF oldenemy = -1 AND .pos.x = 0 AND .pos.y = 0 THEN
       .pos.x = 70
       .pos.y = 95
      END IF
      '.pos.x -= w(slot) \ 2
      '.pos.y -= h(slot)
     END IF
    END WITH
   END IF
  END IF

  ' Draw screen

  IF form.background_frames > 1 AND form.background_ticks > 0 THEN
   bgwait = (bgwait + 1) MOD form.background_ticks
   IF bgwait = 0 THEN
    bgctr = loopvar(bgctr, 0, form.background_frames - 1, 1)
    DIM sl as Slice ptr = LookupSlice(SL_FORMEDITOR_BACKDROP, rootslice)
    ChangeSpriteSlice sl, , (form.background + bgctr) MOD gen(genNumBackdrops)
   END IF
  END IF
  draw_formation_slices form, rootslice, slot, dpage

  IF positioning_mode = NO THEN
   menu(0) = "Previous Menu"
   menu(1) = CHR(27) + "Formation " & form_id & CHR(26)
   menu(2) = "Backdrop: " & form.background
   IF form.background_frames <= 1 THEN
    menu(3) = "Backdrop Animation: none"
    menu(4) = " Ticks per Backdrop Frame: -NA-"
   ELSE
    menu(3) = "Backdrop Animation: " & form.background_frames & " frames"
    menu(4) = " Ticks per Backdrop Frame: " & form.background_ticks
   END IF
   menu(5) = "Battle Music:"
   IF form.music = -2 THEN
     menu(5) &= " -same music as map-"
   ELSEIF form.music = -1 THEN
     menu(5) &= " -silence-"
   ELSEIF form.music >= 0 THEN
     menu(5) &= " " & form.music & " " & getsongname(form.music)
   END IF
   FOR i as integer = 0 TO 7
    menu(6 + i) = "Enemy:" + ename(i)
   NEXT i
   standardmenu menu(), state, 0, 0, dpage, menuopts
  END IF
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP

 SaveFormation form, form_id
 music_stop
 DeleteSlice @rootslice
END SUB

'Deletes previous rootslice if any, then creates a bunch of sprite slices for enemies
'(but doesn't position them: that's done in draw_formation_slices), and rectangles for
'hero positions.
'Also loads enemy names.
SUB load_formation_slices(ename() as string, form as Formation, rootslice as Slice ptr ptr)
 DIM sl as Slice ptr
 DeleteSlice rootslice

 ' Root is backdrop
 *rootslice = NewSliceOfType(slSprite)
 sl = *rootslice
 ChangeSpriteSlice sl, sprTypeMXS, form.background 
 sl->Lookup = SL_FORMEDITOR_BACKDROP
 
 ' Heroes
 FOR i as integer = 0 TO 3
  sl = NewSliceOfType(slRectangle, *rootslice)
  ChangeRectangleSlice sl, , boxlook(0).bgcol, boxlook(0).edgecol
  sl->X = 240 + i * 8
  sl->Y = 82 + i * 20
  sl->Width = 32
  sl->Height = 40
  'Break ties with heroes behind
  sl->Sorter = i
 NEXT

 ' Enemies
 FOR i as integer = 0 TO 7
  ename(i) = "-EMPTY-"
  IF form.slots(i).id >= 0 THEN
   DIM enemy as EnemyDef
   loadenemydata enemy, form.slots(i).id
   WITH enemy
    ename(i) = form.slots(i).id & ":" & .name
    sl = NewSliceOfType(slSprite, *rootslice)
    ChangeSpriteSlice sl, sprTypeSmallEnemy + bound(.size, 0, 2), .pic, .pal
    sl->Lookup = SL_FORMEDITOR_ENEMY + i
    sl->Sorter = 100 + i
   END WITH
  END IF
 NEXT i

 ' Cursor (defaults to invisible)
 sl = NewSliceOfType(slText, *rootslice)
 sl->AlignHoriz = 1  'mid
 sl->AnchorHoriz = 1  'mid
 sl->Lookup = SL_FORMEDITOR_CURSOR
END SUB

SUB draw_formation_slices(form as Formation, rootslice as Slice ptr, selected_slot as integer, page as integer)
 STATIC flash as integer
 flash = (flash + 1) MOD 256
 DIM cursorsl as Slice ptr = LookupSlice(SL_FORMEDITOR_CURSOR, rootslice)
 cursorsl->Visible = NO

 ' Set enemy positions and parent of cursor slice
 DIM sl as Slice ptr = rootslice->FirstChild
 WHILE sl
  IF sl->Lookup >= SL_FORMEDITOR_ENEMY AND sl->Lookup <= SL_FORMEDITOR_LAST_ENEMY THEN
   'Is an enemy
   DIM enemy_slot as integer = sl->Lookup - SL_FORMEDITOR_ENEMY
   DIM fslot as FormationSlot ptr = @form.slots(enemy_slot)
   IF fslot->id < 0 THEN debugc errPromptBug, "Formation enemy slice corresponds to an empty slot"
   sl->X = fslot->pos.x
   sl->Y = fslot->pos.y
   IF enemy_slot = selected_slot AND cursorsl <> NULL THEN
    cursorsl->Visible = YES
    SetSliceParent cursorsl, sl
    ChangeTextSlice cursorsl, CHR(25), flash
   END IF
  END IF
  sl = sl->NextSibling
 WEND

 ' We don't want path-dependence of the sort order, because
 ' the real battle system display breaks ties in a certain way.
 ' This ensures ties broken in the same way (I hope)
 CustomSortChildSlices rootslice, NO  'wipevals=NO
 EdgeYSortChildSlices rootslice, 2  'edge=2 is bottom edge
 clearpage page
 DrawSlice rootslice, page
END SUB

SUB item_editor
 DIM menu() as string
 DIM menu_display() as string
 DIM shaded() as bool

 DIM selectst as SelectTypeState
 DIM state as MenuState
 state.size = 24
 state.first = -1
 state.top = -1
 state.last = gen(genMaxItem) + 1
 state.need_update = YES

 setkeys YES
 DO
  setwait 55
  setkeys YES
  IF keyval(scESC) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "item_editor_pickitem"
  IF keyval(scCtrl) > 0 AND keyval(scBackspace) > 0 AND state.pt >= 0 THEN
   cropafter state.pt, gen(genMaxItem), 0, game + ".itm", getbinsize(binITM)
   load_special_tag_caches
   state.need_update = YES
  END IF
  usemenu state
  IF enter_space_click(state) THEN
   IF state.pt = -1 THEN EXIT DO
   IF state.pt = gen(genMaxItem) + 1 THEN
    IF gen(genMaxItem) < maxMaxItems THEN
     gen(genMaxItem) += 1
     DIM itembuf(dimbinsize(binITM)) as integer
     item_editor_init_new itembuf()
     saveitemdata itembuf(), state.pt
     state.need_update = YES
    END IF
   END IF
   IF state.pt <= gen(genMaxItem) THEN
    individual_item_editor state.pt
    state.need_update = YES
   END IF
  END IF

  IF state.need_update THEN
   state.need_update = NO
   state.last = gen(genMaxItem) + 1

   REDIM shaded(-1 TO state.last)
   FOR i as integer = LBOUND(shaded) TO UBOUND(shaded)
    shaded(i) = NO
   NEXT

   REDIM menu(-1 TO state.last)
   REDIM menu_display(-1 TO state.last)
   menu(-1) = "Return to Main Menu"
   FOR i as integer = 0 TO gen(genMaxItem)
    menu(i) = i & " " & readitemname(i)
   NEXT
   IF gen(genMaxItem) < maxMaxItems THEN
    menu(UBOUND(menu)) = "Add a new item"
   ELSE
    menu(UBOUND(menu)) = "No more items can be added"
    shaded(UBOUND(menu)) = YES
   END IF
  END IF
  IF select_by_typing(selectst) THEN
   select_on_word_boundary menu(), selectst, state
  END IF

  clearpage dpage
  highlight_menu_typing_selection menu(), menu_display(), selectst, state
  standardmenu menu_display(), state, shaded(), 0, 0, dpage
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
END SUB

SUB individual_item_editor(item_id as integer)
 DIM itembuf(dimbinsize(binITM)) as integer
 CONST menusize as integer = 21
 DIM menu(menusize) as string
 DIM menu_display(menusize) as string
 DIM shaded(menusize) as bool
 DIM wep_img as GraphicPair
 DIM box_preview as string = ""
 'DIM ibitnames(-1 TO 59) as string

 DIM eqst(5) as string
 eqst(0) = "NEVER EQUIPPED"
 eqst(1) = readglobalstring(38, "Weapon", 10)
 FOR i as integer = 0 TO 3
  eqst(i + 2) = readglobalstring(25 + i, "Armor" & i+1)
 NEXT i

 loaditemdata itembuf(), item_id
 DIM item_name as string, info as string
 'item_name and info aren't rewritten to itembuf() after changes until the menu is quit
 item_name = readbadbinstring(itembuf(), 0, 8)
 info = readbadbinstring(itembuf(), 9, 36)

 'Indexed by menu index, not by .itm/itembuf() index
 DIM max(menusize) as integer
 DIM min(menusize) as integer
 max(3) = 32767
 max(4) = 99
 max(5) = 5
 max(6) = gen(genMaxAttack)
 max(7) = gen(genMaxAttack)
 max(8) = gen(genMaxAttack)
 max(9) = gen(genMaxAttack)
 max(10) = 2
 max(11) = max_tag()
 max(12) = max_tag()
 max(13) = max_tag()
 max(14) = max_tag()
 max(15) = gen(genMaxWeaponPic)
 min(16) = -1
 max(16) = 32767

 'Map from menu() indices to itembuf() indices
 DIM iidx(menusize) as integer
 iidx(3)  = 46 'value
 iidx(4)  = 210 'stack size
 iidx(5)  = 49 'equippable as
 iidx(6)  = 47 'in battle use
 iidx(7)  = 48 'weapon use
 iidx(8)  = 50 'teach spell
 iidx(9)  = 51 'out of battle use
 iidx(10) = 73 'consumption
 iidx(11) = 74 'own item tag
 iidx(12) = 75 'in inventory tag
 iidx(13) = 76 'equipped tag
 iidx(14) = 77 'actively equipped tag
 iidx(15) = 52 'weapon pic
 iidx(16) = 53 'weapon pal

 DIM selectst as SelectTypeState
 DIM enable_strgrabber as bool
 DIM state as MenuState
 state.size = 24
 state.last = menusize
 state.need_update = YES

 setkeys YES
 DO
  setwait 55
  setkeys YES
  IF keyval(scESC) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "item_editor"
  usemenu state
  enable_strgrabber = NO
  IF LEN(selectst.query) = 0 AND (state.pt = 1 OR state.pt = 2) THEN
   enable_strgrabber = YES
  END IF
  IF enter_space_click(state) THEN
   IF state.pt = 0 THEN EXIT DO
   IF itembuf(49) = 1 THEN
    IF state.pt = 17 THEN
     xy_position_on_sprite wep_img, itembuf(80), itembuf(81), 0, 24, 24, "weapon handle position", "xy_weapon_handle"
     state.need_update = YES
    END IF
    IF state.pt = 18 THEN
     xy_position_on_sprite wep_img, itembuf(78), itembuf(79), 1, 24, 24, "weapon handle position", "xy_weapon_handle"
     state.need_update = YES
    END IF
   END IF
   IF itembuf(49) > 0 THEN
    IF state.pt = 19 THEN
     item_editor_stat_bonuses itembuf()
     state.need_update = YES
    END IF
    IF state.pt = 20 THEN
     item_editor_elementals itembuf()
    END IF
    'IF state.pt = 20 THEN
    ' editbitset itembuf(), 70, 23, ibitnames()
    ' state.need_update = YES
    'END IF
    IF state.pt = 21 THEN
     item_editor_equipbits itembuf()
     state.need_update = YES
    END IF
   END IF
   IF state.pt = 16 THEN '--palette picker
    itembuf(53) = pal16browse(itembuf(53), 5, itembuf(52))
    state.need_update = YES
   END IF
  END IF
  SELECT CASE state.pt
   CASE 1
    IF enable_strgrabber ANDALSO strgrabber(item_name, 8) THEN
     state.need_update = YES
    END IF
   CASE 2
    IF enable_strgrabber ANDALSO strgrabber(info, 36) THEN
     state.need_update = YES
    END IF
   CASE 3, 4, 5, 10, 15, 16
    IF intgrabber(itembuf(iidx(state.pt)), min(state.pt), max(state.pt)) THEN
     state.need_update = YES
    END IF
   CASE 6, 7, 8
    IF zintgrabber(itembuf(iidx(state.pt)), -1, max(state.pt)) THEN
     state.need_update = YES
    END IF
   CASE 9
    IF xintgrabber(itembuf(51), 0, max(state.pt), -1, gen(genMaxTextbox) * -1) THEN
     state.need_update = YES
    END IF
   CASE 11 TO 14
    IF tag_grabber(itembuf(74 + (state.pt - 11)), 0) THEN
     state.need_update = YES
     'Update cache
     itemtags(item_id).have_tag = itembuf(74)
     itemtags(item_id).in_inventory_tag = itembuf(75)
     itemtags(item_id).is_equipped_tag = itembuf(76)
     itemtags(item_id).is_actively_equipped_tag = itembuf(77)
    END IF
  END SELECT
  IF state.need_update THEN
   state.need_update = NO
   generate_item_edit_menu menu(), shaded(), itembuf(), item_name, info, eqst(), box_preview
   load_sprite_and_pal wep_img, sprTypeWeapon, itembuf(52), itembuf(53)
  END IF
  IF enable_strgrabber = NO ANDALSO select_by_typing(selectst, NO) THEN
   select_on_word_boundary menu(), selectst, state
  END IF

  clearpage dpage
  highlight_menu_typing_selection menu(), menu_display(), selectst, state
  standardmenu menu_display(), state, shaded(), 0, 0, dpage
  IF itembuf(49) = 1 THEN
   'Is a weapon
   DIM frame as integer = 0
   IF state.pt = 17 THEN frame = 1
   frame_draw wep_img.sprite + 1 - frame, wep_img.pal, 280, 160, , , dpage
   DIM handle as XYPair
   handle.x = 280 + itembuf(78 + frame * 2)
   handle.y = 160 + itembuf(79 + frame * 2)
   DIM col as integer = uilook(uiSelectedItem) + state.tog
   drawline handle.x - 2, handle.y    , handle.x - 1, handle.y    , col, dpage
   drawline handle.x    , handle.y - 2, handle.x    , handle.y - 1, col, dpage
   drawline handle.x + 1, handle.y    , handle.x + 2, handle.y    , col, dpage
   drawline handle.x    , handle.y + 1, handle.x    , handle.y + 2, col, dpage
  END IF
  edgeprint box_preview, 0, 191, uilook(uiText), dpage
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
 IF wep_img.sprite THEN frame_unload @wep_img.sprite
 IF wep_img.pal    THEN palette16_unload @wep_img.pal
 writebadbinstring item_name, itembuf(), 0, 8
 writebadbinstring info, itembuf(), 9, 36
 saveitemdata itembuf(), item_id
END SUB

SUB generate_item_edit_menu (menu() as string, shaded() as bool, itembuf() as integer, item_name as string, info_string as string, equip_types() as string, byref box_preview as string)
 DIM weapon as string = readglobalstring(38, "Weapon", 10)
 menu(0) = "Back to Item Menu"
 menu(1) = "Name:" & item_name
 menu(2) = "Info:" & info_string
 menu(3) = "Value: " & itembuf(46)
 menu(4) = "Maximum stack size: " & defaultint(itembuf(210), "Default (" & gen(genItemStackSize) & ")", 0)
 menu(5) = "Equippable as: " & equip_types(bound(itembuf(49), 0, 5))
 menu(6) = "When used in battle: " & item_attack_name(itembuf(47))
 menu(7) = "When used as a " & weapon & ": " & item_attack_name(itembuf(48))
 menu(8) = "Teach Spell: " & item_attack_name(itembuf(50))
 IF itembuf(51) >= 0 THEN
  menu(9) = "When used out of battle: " & item_attack_name(itembuf(51))
  box_preview = ""
 ELSE
  menu(9) = "When used out of battle: Text " & ABS(itembuf(51))
  box_preview = textbox_preview_line(ABS(itembuf(51)))
 END IF
 menu(10) = "Unlimited Use"
 IF itembuf(73) = 1 THEN menu(10) = "Consumed By Use"
 IF itembuf(73) = 2 THEN menu(10) = "Cannot be Sold/Dropped"
 menu(11) = "own item TAG " & itembuf(74) & " " & load_tag_name(itembuf(74))
 menu(12) = "is in inventory TAG " & itembuf(75) & " " & load_tag_name(itembuf(75))
 menu(13) = "is equipped TAG " & itembuf(76) & " " & load_tag_name(itembuf(76))
 menu(14) = "eqpt by active hero TAG " & itembuf(77) & " " & load_tag_name(itembuf(77))
 menu(15) = "Weapon Picture: " & itembuf(52)
 menu(16) = "Weapon Palette: " & defaultint(itembuf(53))
 menu(17) = "Handle position A..."
 menu(18) = "Handle position B..."
 menu(19) = "Stat Bonuses..."
 menu(20) = "Elemental Resists..."
 'menu(20) = "Equipment Bits..."
 menu(21) = "Who Can Equip?..."

 FOR i as integer = 0 TO UBOUND(shaded)
  shaded(i) = NO
 NEXT
 IF itembuf(49) <> 1 THEN  'Not a weapon
  menu(7) = "When used as a " & weapon & ": N/A"
  shaded(7) = YES
  menu(15) = "Weapon Picture: N/A"
  menu(16) = "Weapon Palette: N/A"
  shaded(15) = YES
  shaded(16) = YES
  shaded(17) = YES
  shaded(18) = YES
 END IF
 IF itembuf(49) = 0 THEN  'Not equipable
  'Don't N/A the tags, because they still take effect
  shaded(13) = YES
  shaded(14) = YES
  shaded(19) = YES
  shaded(20) = YES
  shaded(21) = YES
 END IF

END SUB

FUNCTION item_attack_name(n as integer) as string
 IF n <= 0 THEN RETURN "NOTHING"
 RETURN n - 1 & " " & readattackname(n - 1)
END FUNCTION

SUB handle_npc_def_delete (npc() as NPCType, byval id as integer, byref num_npc_defs as integer, npc_insts() as NPCInst)

 '--Count number of uses
 DIM as integer uses = 0
 FOR i as integer = 0 to 299
  IF npc_insts(i).id = id + 1 THEN uses += 1
 NEXT

 IF uses > 0 THEN
  IF yesno("There are " & uses & " copies of NPC ID " & id & " on the map. Are you sure you want to delete them?", NO, NO) = NO THEN EXIT SUB

  '--Delete instances of this ID
  FOR i as integer = 0 to UBOUND(npc_insts)
   IF npc_insts(i).id = id + 1 THEN npc_insts(i).id = 0
  NEXT
 END IF

 '--Wiping a definition clear, or completely deleting it?
 DIM as integer deleting = NO
 '--Can't delete ID 0; must always have at least one NPC
 IF id > 0 AND id = num_npc_defs - 1 THEN deleting = YES

 IF yesno(iif_string(uses, "Done. ", "") & "Really " & iif_string(deleting, "delete", "wipe clean") & " this NPC definition?", NO, NO) = NO THEN EXIT SUB

 IF deleting THEN
  num_npc_defs -= 1
 ELSE
  CleanNPCDefinition npc(id)
 END IF

END SUB

SUB npcdef (st as MapEditState, npc_img() as GraphicPair, gmap() as integer, zmap as ZoneMap)
'npc_img() should be of fixed size (0 TO max_npc_defs - 1), like st.npc_def(), with the actual number passed in st.num_npc_defs

DIM boxpreview(st.num_npc_defs - 1) as string
DIM need_update_selected as bool = NO

DIM state as MenuState
state.size = 7

'--If there's room for more, add "Add new NPC" option to end
state.last = st.num_npc_defs - 1
IF st.num_npc_defs < max_npc_defs THEN state.last += 1

FOR i as integer = 0 TO st.num_npc_defs - 1
 boxpreview(i) = textbox_preview_line(st.npc_def(i).textbox)
NEXT i
setkeys YES
DO
 setwait 55
 setkeys YES
 state.tog = state.tog XOR 1
 IF keyval(scESC) > 1 THEN EXIT DO
 IF keyval(scF1) > 1 THEN show_help "pick_npc_to_edit"
 intgrabber state.pt, 0, state.last
 usemenu state
 IF enter_space_click(state) THEN
  IF state.pt = st.num_npc_defs THEN
   '--Add new NPC option
   st.num_npc_defs += 1
   CleanNPCDefinition st.npc_def(st.num_npc_defs - 1)
  ELSE
   '--An NPC
   'First save NPCs so that we can correctly search for unused one-time use tags (see onetimetog)
   SaveNPCD_fixedlen maplumpname(st.mapnum, "n"), st.npc_def(), st.num_npc_defs
   edit_npc st.npc_def(state.pt), gmap(), zmap
  END IF
  need_update_selected = YES
 END IF
 IF keyval(scPlus) > 1 THEN
  '--Fast add button (for people who really want ID 134 for a task)
  st.num_npc_defs += 1
  CleanNPCDefinition st.npc_def(st.num_npc_defs - 1)
  state.pt = st.num_npc_defs - 1
  need_update_selected = YES
 END IF
 IF keyval(scDelete) > 1 THEN
  '--This updates st.num_npc_defs as needed, but not state.pt
  handle_npc_def_delete st.npc_def(), state.pt, st.num_npc_defs, st.npc_inst()
  IF state.pt > st.num_npc_defs - 1 THEN
   '--Deleted last NPC def
   frame_unload @npc_img(state.pt).sprite
   palette16_unload @npc_img(state.pt).pal
   state.pt = st.num_npc_defs - 1
  END IF
  need_update_selected = YES
 END IF

 IF need_update_selected THEN
  '--Note not all, or even any, of these updates will be required in a given case
  '--Re-load the picture and palette
  WITH npc_img(state.pt)
   IF .sprite THEN frame_unload(@.sprite)
   .sprite = frame_load(4, st.npc_def(state.pt).picture)
   IF .pal THEN palette16_unload(@.pal)
   .pal = palette16_load(st.npc_def(state.pt).palette, 4, st.npc_def(state.pt).picture)
  END WITH
  '--Update box preview line
  REDIM PRESERVE boxpreview(st.num_npc_defs - 1)
  boxpreview(state.pt) = textbox_preview_line(st.npc_def(state.pt).textbox)
  '--Update menu size
  state.last = st.num_npc_defs - 1
  IF st.num_npc_defs < max_npc_defs THEN state.last += 1

  need_update_selected = NO
 END IF

 clearpage dpage
 FOR i as integer = state.top TO state.top + 7
  IF i > state.last THEN EXIT FOR
  IF state.pt = i THEN edgebox 0, (i - state.top) * 25, 320, 22, uilook(uiDisabledItem), uilook(uiMenuItem), dpage
  textcolor uilook(uiMenuItem), 0
  IF state.pt = i THEN textcolor uilook(uiSelectedItem + state.tog), 0
  IF i > st.num_npc_defs - 1 THEN
   '--Add new NPC option
   printstr "Add new NPC", 0, ((i - state.top) * 25) + 5, dpage
  ELSE
   '--An NPC
   printstr "" & i, 0, ((i - state.top) * 25) + 5, dpage
   WITH npc_img(i)
    '--Down A frame
    frame_draw .sprite + 4, .pal, 32, (i - state.top) * 25, 1, -1, dpage
   END WITH
   textcolor uilook(uiMenuItem), uilook(uiHighlight)
   IF state.pt = i THEN textcolor uilook(uiText), uilook(uiHighlight)
   printstr boxpreview(i), 56, ((i - state.top) * 25) + 5, dpage
  END IF
 NEXT i
 SWAP vpage, dpage
 setvispage vpage
 dowait
LOOP

END SUB

'--Hero Editor stuff---------------------------------------------------

SUB hero_editor_load_hero (st as HeroEditState, her as HeroDef, byval hero_id as integer)
 loadherodata her, hero_id
 enforce_hero_data_limits her
 update_hero_preview_pics st, her
END SUB

SUB hero_editor
 DIM hero_id as integer = 0
 DIM her as HeroDef
 DIM st as HeroEditState
 WITH st
  .preview_walk_direction = 1
  .preview_steps = 0
  .preview_walk_pos.x = 0
  .preview_walk_pos.y = 0
  .previewframe = -1
 END WITH

 DIM menu(10) as string
 menu(0) = "Return to Main Menu"
 menu(1) = CHR(27) & "Pick Hero " & hero_id & CHR(26)
 menu(2) = "Name:"
 menu(3) = "Appearance and Misc..."
 menu(4) = "Edit Stats..."
 menu(5) = "Edit Spell Lists..."
 menu(6) = "Edit Battle Menu..."
 menu(7) = "Bitsets..."
 menu(8) = "Elemental Resistances..."
 menu(9) = "Hero Tags..."
 menu(10) = "Equipment..."

 DIM mstate as MenuState
 WITH mstate
  .pt = 1
  .size = 22
  .last = UBOUND(menu)
 END WITH

 DIM hbit(-1 TO 26) as string
 'Lots of obsolete elemental bits
 hbit(24) = "Rename when added to party"
 hbit(25) = "Permit renaming on status screen"
 hbit(26) = "Do not show spell lists if empty"

 hero_editor_load_hero st, her, hero_id

 setkeys YES
 DO
  setwait 55
  setkeys YES
  animate_hero_preview st
  IF keyval(scESC) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "hero_editor"
  IF keyval(scCtrl) > 0 AND keyval(scBackspace) > 0 THEN
   cropafter hero_id, gen(genMaxHero), -1, game + ".dt0", getbinsize(binDT0)
   load_special_tag_caches
  END IF
  usemenu mstate
  IF enter_space_click(mstate) THEN
   SELECT CASE mstate.pt
    CASE 0: EXIT DO
    CASE 3: hero_editor_appearance st, her
    CASE 4: hero_editor_stats_menu her
    CASE 5: hero_editor_spell_lists_toplevel her
    CASE 6: hero_editor_battle_menu her
    CASE 7: editbitset her.bits(), 0, 26, hbit()
    CASE 8: hero_editor_elementals her
    CASE 9: hero_editor_tags hero_id, her
    CASE 10: hero_editor_equipment_list hero_id, her
   END SELECT
  END IF

  IF mstate.pt = 1 THEN
   DIM remem_hero_id as integer = hero_id
   IF intgrabber(hero_id, 0, gen(genMaxHero), scLeftCaret, scRightCaret) THEN
    saveherodata her, remem_hero_id
    hero_editor_load_hero st, her, hero_id
   END IF
   IF keyval(scLeft) > 1 AND hero_id > 0 THEN
    saveherodata her, hero_id
    hero_id -= 1
    hero_editor_load_hero st, her, hero_id
   END IF
   IF keyval(scRight) > 1 AND hero_id < 59 THEN
    saveherodata her, hero_id
    hero_id += 1
    IF needaddset(hero_id, gen(genMaxHero), "hero") THEN
     gen(genMaxHero) -= 1  'Incremented by both needaddset and hero_editor_add_new
     IF hero_editor_add_new(st, her, hero_id) THEN
      saveherodata her, hero_id
     END IF
    END IF
    hero_editor_load_hero st, her, hero_id
   END IF
  END IF
  IF mstate.pt = 2 THEN
   strgrabber her.name, 16
  END IF

  menu(1) = CHR(27) & "Pick Hero " & hero_id & CHR(26)
  menu(2) = "Name:" & her.name

  '--Draw screen
  clearpage dpage
  standardmenu menu(), mstate, 0, 0, dpage

  draw_hero_preview st, her
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
 saveherodata her, hero_id
 clear_hero_preview_pics st
END SUB

FUNCTION hero_editor_add_new (st as HeroEditState, her as HeroDef, byref recindex as integer) as bool
 'Returns YES if a new hero was added, or NO if the add was cancelled
  DIM menu(2) as string
  DIM herotocopy as integer = 0
  DIM state as MenuState
  state.last = UBOUND(menu)
  state.size = 24
  state.pt = 1

  state.need_update = YES
  setkeys
  DO
    setwait 55
    animate_hero_preview st
    setkeys
    IF keyval(scESC) > 1 THEN  'cancel
      recindex -= 1
      EXIT DO
    END IF
    IF keyval(scF1) > 1 THEN show_help "hero_new"
    usemenu state
    IF state.pt = 2 THEN
      IF intgrabber(herotocopy, 0, gen(genMaxHero)) THEN state.need_update = YES
    END IF
    IF state.need_update THEN
      state.need_update = NO
      loadherodata her, herotocopy
      update_hero_preview_pics st, her
      menu(0) = "Cancel"
      menu(1) = "New Blank Hero"
      menu(2) = "Copy of Hero " & herotocopy & " " & her.name
    END IF
    IF enter_space_click(state) THEN
      SELECT CASE state.pt
        CASE 0 ' cancel
          recindex -= 1
          RETURN NO
        CASE 1 ' blank
          gen(genMaxHero) += 1
          ClearHeroData her
          RETURN YES
        CASE 2 ' copy
          gen(genMaxHero) += 1
          RETURN YES
      END SELECT
    END IF

    clearpage dpage
    standardmenu menu(), state, 0, 0, dpage
    IF state.pt = 2 THEN
      draw_hero_preview st, her
    END IF
    setvispage dpage
    dowait
  LOOP
END FUNCTION

SUB hero_editor_stat_visibility (hero as HeroDef)
 DIM tmpbits(0) as integer
 REDIM menu(statLast) as string
 getstatnames menu()
 FOR i as integer = 0 TO statLast
  menu(i) = "Hide " & menu(i)
 NEXT i
 
 DIM hero_node as NodePtr
 hero_node = hero.reld
 READNODE hero_node."stat_options" as opt
  WITHNODE opt."stat" as stat
   DIM i as integer = GetInteger(stat)
   IF stat."hide".exists THEN setbit tmpbits(), 0, i, YES
  END WITHNODE
 END READNODE

 editbitset tmpbits(), 0, statLast, menu(), "hero_stat_visibility"
 
 DIM stat_options as NodePtr
 stat_options = hero_node."stat_options".ptr
 IF stat_options = 0 THEN stat_options = SetChildNode(hero_node, "stat_options")

 DIM stat as NodePtr
 FOR i as integer = 0 TO statLast
  stat = NodeByPath(stat_options, "/stat[" & i & "]")
  IF stat = 0 THEN
   'Node does not exist yet
   IF xreadbit(tmpbits(), i) THEN
    stat = AppendChildNode(stat_options, "stat", i)
    SetChildNode(stat, "hide")
   END IF
  ELSE
   'Node already exists
   IF xreadbit(tmpbits(), i) THEN
    SetChildNode(stat, "hide")
   ELSE
    IF stat."hide".exists THEN FreeNode stat."hide".ptr
   END IF
  END IF
 NEXT i
 
END SUB

SUB hero_editor_stats_menu (her as HeroDef)
 DIM i as integer
 DIM activemenu as integer = 0  '0 or 1 indicating left or rightmenu
 DIM twomenus as bool = (gen(genMaxLevel) > 0)  'whether to draw the second menu

 '.unselectable attribute only used from leftmenu
 DIM as BasicMenuItem vector leftmenu, rightmenu
 v_new leftmenu, 4 + 12
 v_new rightmenu, 4 + 12
 leftmenu[0].text = "Previous Menu"
 leftmenu[1].text = "Stat Visibility..."
 leftmenu[2].unselectable = YES
 leftmenu[3].text = "LEVEL ZERO"
 leftmenu[3].unselectable = YES
 leftmenu[3].col = uilook(uiDescription)
 ' FIXME: the rightmost column is always the level 99 stats rather than gen(genMaxLevel)
 rightmenu[3].text = "LEVEL 99"
 rightmenu[3].col = uilook(uiDescription)

 DIM mstate as MenuState
 WITH mstate
  .last = v_len(leftmenu) - 1
  .size = 22
  .need_update = YES
  .active = YES
 END WITH
 DIM inactive_state as MenuState
 WITH inactive_state
  .last = mstate.last
  .size = mstate.size
  .active = NO
 END WITH

 DIM min(11) as integer, max(11) as integer
 FOR i as integer = 0 TO UBOUND(min)
  min(i) = 0
  max(i) = 999
 NEXT
 max(statHP) = 9999
 max(statMP) = 9999
 max(statFocus) = 100
 max(statHitX) = 10
 
 DIM mouse as MouseInfo

 setkeys
 DO
  setwait 55
  setkeys
  IF keyval(scESC) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "hero_stats"

  usemenu mstate, leftmenu
  DIM statnum as integer = mstate.pt - 4
  
  mouse = readmouse
  IF mouse.movedtick THEN
   IF mouse.x > 160 AND twomenus THEN
    activemenu = 1
   ELSE
    activemenu = 0
   END IF
  END IF

  IF enter_space_click(mstate) THEN
   IF mstate.pt = 0 THEN EXIT DO
   IF mstate.pt = 1 THEN
    hero_editor_stat_visibility her
    mstate.need_update = YES
   END IF
  END IF
  IF statnum < 0 THEN
   activemenu = 0
  ELSE
   IF twomenus AND (keyval(scLeft) > 1 OR keyval(scRight) > 1) THEN activemenu XOR= 1
   IF activemenu = 0 THEN
    IF intgrabber(her.Lev0.sta(statnum), min(statnum), max(statnum), scLeftCaret, scRightCaret) THEN mstate.need_update = YES
   ELSE
    IF intgrabber(her.LevMax.sta(statnum), min(statnum), max(statnum), scLeftCaret, scRightCaret) THEN mstate.need_update = YES
   END IF
  END IF

  IF mstate.need_update THEN
   mstate.need_update = NO
   FOR i as integer = 0 TO UBOUND(her.Lev0.sta)
    WITH leftmenu[4 + i]
     .text = statnames(i) & " " & her.Lev0.sta(i)
     .col = uiLook(uiMenuItem)
     IF should_hide_hero_stat(her, i) THEN .col = uiLook(uiDisabledItem)
    END WITH
    WITH rightmenu[4 + i]
     .text = statnames(i) & " " & her.LevMax.sta(i)
     .col = uiLook(uiMenuItem)
     IF should_hide_hero_stat(her, i) THEN .col = uiLook(uiDisabledItem)
    END WITH
   NEXT i
  END IF

  '--Draw screen
  clearpage dpage
  'Note that exactly one of the following two standardmenus should be active, since they share 'state'
  IF activemenu = 0 THEN
   standardmenu leftmenu, mstate, 8, 0, dpage
   IF twomenus THEN standardmenu rightmenu, inactive_state, 160, 0, dpage
  ELSE 'activemenu = 1
   standardmenu leftmenu, inactive_state, 8, 0, dpage
   IF twomenus THEN standardmenu rightmenu, mstate, 160, 0, dpage
  END IF

  IF statnum >= 0 THEN  '--Draw graph
   textcolor uilook(uiMenuItem), 0
   printstr statnames(statnum), 310 - LEN(statnames(statnum)) * 8, 160, dpage
   IF twomenus THEN
    DIM levelmsg as string
    levelmsg = "Levels 0-" & gen(genMaxLevel)
    printstr levelmsg, 310 - LEN(levelmsg) * 8, 170, dpage
   END IF
   DIM as integer n0, nMax, numbars, barwidth
   IF gen(genMaxLevel) < 25 THEN
    numbars = gen(genMaxLevel) + 1
    barwidth = 25 \ numbars
   ELSE
    numbars = 25
    barwidth = 1
   END IF
   n0 = her.Lev0.sta(statnum)
   nMax = her.LevMax.sta(statnum)
   FOR i as integer = 0 TO numbars - 1
    DIM as integer level, ordinate
    level = INT(i * gen(genMaxLevel) / large(1, numbars - 1))  'round result to avoid error
    ordinate = atlevel(level, n0, nMax) * (100 / max(statnum))
    rectangle 290 + i * barwidth, 156 - ordinate, barwidth, ordinate + 1, uilook(uiMenuItem), dpage
   NEXT i

   'Experimental stuff
   IF keyval(scLeftShift) > 1 THEN
    debug "0=" & atlevel(0, n0, nMax) & " max(" & gen(genMaxLevel) & ")=" & atlevel(gen(genMaxLevel), n0, nMax) & " 99=" & atlevel(99, n0, nMax)
   END IF
   IF keyval(scF7) > 1 AND gen(genMaxLevel) > 1 THEN gen(genMaxLevel) -= 1 : debug "gen(genMaxLevel)=" & gen(genMaxLevel)
   IF keyval(scF8) > 1 THEN gen(genMaxLevel) += 1 : debug "gen(genMaxLevel)=" & gen(genMaxLevel)
  END IF

  IF statnum = statSpeed THEN
   textcolor uilook(uiDescription), 0
   printstr "Lev0:  1 turn every " & speed_estimate(her.Lev0.spd), 0, 182, dpage
   IF twomenus THEN
    printstr "Lev" & gen(genMaxLevel) & ": 1 turn every " & speed_estimate(her.LevMax.spd), 0, 190, dpage
   END IF
  END IF
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
 v_free leftmenu
 v_free rightmenu
END SUB

SUB hero_editor_spell_lists_toplevel (her as HeroDef)
 DIM spell_list_types(3) as string
 spell_list_types(0) = ""
 spell_list_types(1) = "Level-MP (FF1 Style)"
 spell_list_types(2) = "Random Effects"

 DIM menu(4) as string
 menu(0) = "Previous Menu"

 DIM mstate as MenuState
 WITH mstate
  .pt = 0
  .first = 0
  .last = UBOUND(menu)
  .size = 24
 END WITH

 DIM hint as string
 
 setkeys YES
 DO
  setwait 55
  setkeys YES
  IF keyval(scESC) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "hero_spell_menu_types"
  usemenu mstate
  DIM listnum as integer = mstate.pt - 1
  IF mstate.pt >= 1 THEN
   strgrabber her.list_name(listnum), 10
  END IF
  IF keyval(scEnter) > 1 THEN
   IF mstate.pt = 0 THEN EXIT DO
   IF mstate.pt >= 1 AND mstate.pt <= 4 THEN
    IF LEN(her.list_name(listnum)) > 0 THEN
     hero_editor_edit_spell_list her, listnum
    END IF
   END IF
  END IF

  FOR i as integer = 0 TO 3
   menu(1 + i) =  "Spell list " & i & ": " & her.list_name(i)
  NEXT i
  clearpage dpage
  standardmenu menu(), mstate, 0, 0, dpage
  IF listnum >= 0 THEN
   edgeprint spell_list_types(her.list_type(listnum)), 0, 180, uilook(uiSelectedDisabled), dpage
   IF LEN(her.list_name(listnum)) > 0 THEN
    hint = "Press ENTER to edit spells in this list."
   ELSE
    hint = "Type a name for this spell list."
   END IF
   edgeprint hint, 0, 190, uilook(uiDisabledItem), dpage
  END IF
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
END SUB

SUB hero_editor_edit_spell_list (her as HeroDef, byval listnum as integer)

 DIM menu(2) as string
 DIM spellnames(23) as string
 DIM col as integer
 DIM bgcol as integer

 DIM root as Slice Ptr
 root = NewSliceOfType(slRoot)
 WITH *root
  .Fill = YES
 END WITH
 DIM spellbox as Slice Ptr
 spellbox = NewSliceOfType(slRectangle, root)
 WITH *spellbox
  .x = 6
  .y = 34
  .width = 308
  .height = 80
  ChangeRectangleSlice spellbox, 1
 END WITH
 DIM infobox as Slice Ptr
 RefreshSliceScreenPos root
 infobox = NewSliceOfType(slText, root)
 ChangeTextSlice infobox, "", , , YES ' make wrapping
 WITH *infobox
  .Width = root->Width
  .AlignVert = 2
  .AnchorVert = 2
 END WITH
 
 DIM info as string

 DIM mstate as MenuState
 WITH mstate
  .first = -3
  .pt = -3
  .top = -3
  .last = UBOUND(spellnames)
  .need_update = YES
 END WITH
 menu(0) = "Previous Menu..."

 setkeys YES
 DO
  setwait 55
  setkeys YES
  IF keyval(scF1) > 1 THEN show_help "hero_spells"
  IF keyval(scEsc) > 1 THEN EXIT DO
  
  IF mstate.pt >= 0 THEN
   IF usemenu(mstate, scLeft, scRight) THEN
    mstate.need_update = YES
   ELSE
    IF keyval(scUp) > 1 THEN
     mstate.pt -= 3
     IF mstate.pt < 0 THEN mstate.pt = -1
     mstate.need_update = YES
    ELSEIF keyval(scDown) > 1 THEN
     mstate.pt += 3
     IF mstate.pt > mstate.last THEN mstate.pt = mstate.first
     mstate.need_update = YES
    END IF
   END IF
  ELSE
   IF usemenu(mstate) THEN mstate.need_update = YES
  END IF

  IF enter_space_click(mstate) THEN
   IF mstate.pt = -3 THEN
    EXIT DO
   END IF
   IF mstate.pt >= 0 THEN
    hero_editor_edit_spell_list_slot her.spell_lists(listnum, mstate.pt)
    mstate.need_update = YES
   END IF
  END IF
  
  IF mstate.pt = -2 THEN
   IF strgrabber(her.list_name(listnum), 10) THEN mstate.need_update = YES
  END IF

  IF mstate.pt = -1 THEN
   IF intgrabber(her.list_type(listnum), 0, 2) THEN mstate.need_update = YES
  END IF

  IF mstate.pt >= 0 THEN
   IF zintgrabber(her.spell_lists(listnum, mstate.pt).attack, -1, gen(genMaxAttack), scLeftCaret, scRightCaret) THEN mstate.need_update = YES
  END IF

  IF mstate.need_update THEN
   menu(1) = "Spell list name: " & her.list_name(listnum)
   menu(2) = "List Type: "
   SELECT CASE her.list_type(listnum)
    CASE 0: menu(2) &= "Normal"
    CASE 1: menu(2) &= "Use Level-MP (FF1-Style)"
    CASE 2: menu(2) &= "Random Effects"
    CASE ELSE: menu(2) &= "???Unknown " & her.list_type(listnum)
   END SELECT
   FOR i as integer = 0 TO UBOUND(spellnames)
    WITH her.spell_lists(listnum, i)
     IF .attack > 0 THEN
      spellnames(i) = readattackname(.attack - 1)
     ELSE
      spellnames(i) = ""
     END IF
     spellnames(i) = rpad(spellnames(i), " ",  10)
    END WITH
   NEXT i
   info = ""
   IF mstate.pt >= 0 THEN
    WITH her.spell_lists(listnum, mstate.pt)
     IF .attack > 0 THEN
      info &= "Attack ID = " & (.attack - 1) & !"\n"
      IF .learned > 0 THEN
       info &= "Learned at level " & (.learned - 1) & !"\n"
      ELSE
       info &= "Learned from an item" & !"\n"
      END IF
     END IF
    END WITH
    IF her.list_type(listnum) = 1 THEN
     info &= "1 point of level " & INT(mstate.pt / 3) + 1 & " " & readglobalstring(160, "Level MP", 20)
    END IF
   END IF
   ChangeTextSlice infobox, info
   mstate.need_update = NO
  END IF

  '--Draw screen
  mstate.tog XOR= 1
  clearpage dpage
  drawslice root, dpage
  FOR i as integer = 0 TO 2
   col = uiLook(uiMenuItem)
   IF mstate.pt + 3 = i THEN col = uiLook(uiSelectedItem) + mstate.tog
   edgeprint menu(i), 0, 10 * i, col, dpage
  NEXT i
  FOR i as integer = 0 TO mstate.last
   col = uiLook(uiMenuItem)
   bgcol = 0
   IF mstate.pt = i THEN
    col = uiLook(uiSelectedItem) + mstate.tog
    bgcol = uilook(uiHighlight)
   END IF
   textcolor col, bgcol
   printstr spellnames(i), 12 + (i MOD 3) * 104, 40 + (i \ 3) * 8, dpage
  NEXT i

  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
 
 DeleteSlice @root
END SUB

SUB hero_editor_edit_spell_list_slot (slot as SpellList)

 DIM menu as MenuDef
 ClearMenuData menu
 append_menu_item menu, "Previous menu..."
 append_menu_item menu, "Attack:"
 append_menu_item menu, "Learned at level:"
 append_menu_item menu, "Learned from an item:"
 menu.align = -1
 
 DIM st as MenuState
 init_menu_state st, menu
 st.pt = 1
 st.need_update = YES
 st.active = YES

 DIM cap as string = ""

 DIM holdscreen as integer
 holdscreen = allocatepage
 copypage vpage, holdscreen

 setkeys
 DO
  setwait 55
  setkeys
  IF keyval(scF1) > 1 THEN show_help "hero_spells_slot"
  IF keyval(scEsc) > 1 THEN EXIT DO
  
  usemenu st

  SELECT CASE st.pt
   CASE 0:
    IF enter_space_click(st) THEN
     EXIT DO
    END IF
   CASE 1:
    IF zintgrabber(slot.attack, -1, gen(genMaxAttack)) THEN st.need_update = YES
   CASE 2:
    IF zintgrabber(slot.learned, -1, 99) THEN st.need_update = YES
   CASE 3:
    IF enter_space_click(st) ORELSE keyval(scLeft) > 1 ORELSE keyval(scRight) > 1 THEN
     IF slot.learned > 0 THEN
      slot.learned = 0
     ELSE
      slot.learned = 1
     END IF
     st.need_update = YES
    END IF
  END SELECT
  
  IF st.need_update THEN
   cap = ""
   IF slot.attack > 0 THEN cap = (slot.attack - 1) & " " & readattackname(slot.attack - 1)
   menu.items[1]->caption = "Attack: " & cap

   cap = ""
   IF slot.learned > 0 THEN cap = STR(slot.learned - 1)
   menu.items[2]->caption = "Learned at level: " & cap

   cap = "NO"
   IF slot.learned = 0 THEN cap = "YES"
   menu.items[3]->caption = "Learned from an item: " & cap
   
   st.need_update = NO
   copypage holdscreen, vpage
  END IF

  draw_menu menu, st, vpage
  setvispage vpage
  
  dowait
 LOOP

 freepage holdscreen
 setkeys
END SUB

SUB hero_editor_battle_menu (her as HeroDef)
 DIM menu as MenuDef
 DIM preview as MenuDef
  
 DIM st as MenuState
 st.active = YES
 st.need_update = YES
 
 DIM prst as MenuState
 
 DIM hero_node as NodePtr = her.reld
 DIM edit_node as NodePtr

 DIM itembuf(dimbinsize(binITM)) as integer

 setkeys
 DO
  setwait 55
  setkeys
  IF keyval(scF1) > 1 THEN show_help "hero_battle_menu_edit"
  IF keyval(scEsc) > 1 THEN EXIT DO

  IF st.need_update THEN
   ClearMenuData menu
   WITH menu
    .no_box = YES
    .align = -1
    .anchor.x = -1
    .anchor.y = -1
    .offset.x = -160
    .offset.y = -100
    .bordersize = -8
   END WITH
   ClearMenuData preview
   WITH preview
    .align = -1
    .anchor.x = 1
    .anchor.y = -1
    .offset.x = 154
    .offset.y = -86
    .bordersize = -5
    .itemspacing = -2
    .highlight_selection = YES
    .min_chars = 10
    .maxrows = 22
   END WITH
   append_menu_item menu, "Previous menu..."
   READNODE hero_node."battle_menus" as batmen, ignoreall
    WITHNODE batmen."menu" as bmenu
     DIM k as NodePtr = bmenu."kind".ptr
     IF k."weapon".exists THEN
      append_menu_item menu, "Weapon"
      menu.last->dataptr = bmenu
      loaditemdata itembuf(), her.def_weapon
      DIM atk_id as integer = itembuf(48) - 1
      IF atk_id < 0 THEN atk_id = 0
      append_menu_item preview, readattackname(atk_id)
     ELSEIF k."items".exists THEN
      append_menu_item menu, "Items"
      menu.last->dataptr = bmenu
      append_menu_item preview, readglobalstring(34, "Item", 10)
     ELSEIF k."spells".exists THEN
      DIM listname as string = her.list_name(k."spells".integer)
      append_menu_item menu, "Spell List " & k."spells".integer & " " & listname
      menu.last->dataptr = bmenu
      IF LEN(listname) THEN
       append_menu_item preview, listname
      END IF
     ELSEIF k."attack".exists THEN
      append_menu_item menu, "Attack " & k."attack".integer & " " & readattackname(k."attack".integer)
      menu.last->dataptr = bmenu
      append_menu_item preview, readattackname(k."attack".integer)
     ELSE
      debug "Unknown battle menu item " & GetInteger(bmenu)
      append_menu_item menu, "???"
      menu.last->dataptr = bmenu
     END IF
    END WITHNODE
   END READNODE
   init_menu_state st, menu
   init_menu_state prst, preview
   st.need_update = NO
  END IF
  
  menu_of_reorderable_nodes st, menu
  usemenu st
  
  IF enter_space_click(st) THEN
   IF st.pt = 0 THEN EXIT DO
   edit_node = menu.items[st.pt]->dataptr
   edit_battle_menu_item her, edit_node
   st.need_update = YES
  END IF

  IF keyval(scPlus) > 1  ORELSE keyval(scNumPadPlus) > 1 ORELSE keyval(scInsert) > 1 THEN
   DIM ins_node as NodePtr
   ins_node = AppendChildNode(hero_node."battle_menus".ptr, "menu")
   DIM other_node as NodePtr
   other_node = menu.items[st.pt]->dataptr
   IF other_node = 0 THEN other_node = FirstChild(hero_node."battle_menus".ptr)
   IF other_node THEN
    'FIXME: why doesn't this work??
    AddSiblingAfter(other_node, ins_node)
   END IF
   DIM k as NodePtr
   k = SetChildNode(ins_node, "kind")
   SetChildNode(k, "attack", 0)
   st.need_update = YES
  END IF
  
  IF keyval(scDelete) > 1 THEN
   DIM del_node as NodePtr
   del_node = menu.items[st.pt]->dataptr
   IF del_node THEN
    IF yesno("Really delete this menu item?", NO, NO) THEN
     FreeNode del_node
     st.need_update = YES
    END IF
   END IF
  END IF
    
  clearpage dpage
  edgeprint "Preview", 256, 0, uilook(uiText), dpage
  edgeprint "INSERT=add, DEL=delete, F1=help", 0, 190, uilook(uiSelectedDisabled), dpage
  draw_menu preview, prst, dpage
  draw_menu menu, st, dpage
  SWAP vpage, dpage
  setvispage vpage
  
  dowait
 LOOP
 
END SUB

CONST edbatmenu_EXIT = 0
CONST edbatmenu_KIND = 1
CONST edbatmenu_NUM = 2 'extra 0 = lower limit, extra 1 = upper limit

SUB edit_battle_menu_item (hero as HeroDef, byval edit_node as NodePtr)
 IF edit_node = 0 THEN debug "edit_battle_menu_item null NodePtr": EXIT SUB

 DIM holdscreen as integer
 holdscreen = allocatepage
 copypage vpage, holdscreen

 DIM menu as MenuDef
 DIM st as MenuState
 st.active = YES
 st.need_update = YES
 
 DIM k as NodePtr
 DIM v as integer

 setkeys
 DO
  setwait 55
  setkeys
  IF keyval(scF1) > 1 THEN show_help "hero_battle_menu_item"
  IF keyval(scEsc) > 1 THEN EXIT DO

  IF st.need_update THEN
   ClearMenuData menu
   WITH menu
    .boxstyle = 1
    .align = -1
    .min_chars = 36
   END WITH
   append_menu_item menu, "Previous menu...", edbatmenu_EXIT
   k = edit_node."kind".ptr
   IF k."weapon".exists THEN
    append_menu_item menu, "Kind: Weapon's Attack", edbatmenu_KIND
   ELSEIF k."attack".exists THEN
    append_menu_item menu, "Kind: Specific Attack", edbatmenu_KIND
    v = k."attack".integer
    append_menu_item menu, "Attack: " & v & " " & readattackname(v), edbatmenu_NUM
    menu.last->dataptr = k."attack".ptr
    menu.last->extra(0) = 0
    menu.last->extra(1) = gen(genMaxAttack)
   ELSEIF k."spells".exists THEN
    append_menu_item menu, "Kind: Spell List", edbatmenu_KIND
    v = k."spells".integer
    append_menu_item menu, "List Number: " & v & " " & hero.list_name(v), edbatmenu_NUM
    menu.last->dataptr = k."spells".ptr
    menu.last->extra(0) = 0
    menu.last->extra(1) = 3
   ELSEIF k."items".exists THEN
    append_menu_item menu, "Kind: Items Menu", edbatmenu_KIND
   ELSE
    debug "Unknown battle menu kind"
    append_menu_item menu, "Kind: ???", edbatmenu_KIND
   END IF

   init_menu_state st, menu
   st.need_update = NO
  END IF
  
  usemenu st
  
  WITH *menu.items[st.pt]
   IF enter_space_click(st) THEN
    SELECT CASE .t
     CASE edbatmenu_EXIT: EXIT DO
     CASE edbatmenu_KIND:
      browse_battle_menu_kinds edit_node
      st.need_update = YES
    END SELECT
   END IF
   IF .t = edbatmenu_NUM THEN
    v = GetInteger(.dataptr)
    IF intgrabber(v, .extra(0), .extra(1)) THEN
     SetContent(.dataptr, v)
     st.need_update = YES
    END IF
   END IF
  END WITH
  
  copypage holdscreen, vpage
  fuzzyrect 0, 0, 320, 200, uilook(uiBackground), vpage
  draw_menu menu, st, vpage
  setvispage vpage
  
  dowait
 LOOP

 freepage holdscreen
 setkeys
END SUB

SUB browse_battle_menu_kinds (byval menu_node as NodePtr)
 IF menu_node = 0 THEN debug "browse_battle_menu_kinds null NodePtr": EXIT SUB

 DIM holdscreen as integer
 holdscreen = allocatepage
 copypage vpage, holdscreen

 DIM menu as MenuDef
 DIM st as MenuState
 st.active = YES
 st.need_update = YES
 
 setkeys
 DO
  setwait 55
  setkeys
  IF keyval(scF1) > 1 THEN show_help "browse_battle_menu_kinds"
  IF keyval(scEsc) > 1 THEN EXIT DO
  
  IF st.need_update THEN
   ClearMenuData menu
   append_menu_item menu, "Previous Menu...", 0
   append_menu_item menu, "Weapon's Attack", 1
   append_menu_item menu, "Specific Attack", 2
   append_menu_item menu, "Spell List", 3
   append_menu_item menu, "Items Menu", 4
   init_menu_state st, menu
   st.need_update = NO
  END IF

  usemenu st
  
  IF enter_space_click(st) THEN
   WITH *menu.items[st.pt]
    SELECT CASE .t
     CASE 1: change_battle_menu_kind menu_node, "weapon"
     CASE 2: change_battle_menu_kind menu_node, "attack", YES
     CASE 3: change_battle_menu_kind menu_node, "spells", YES
     CASE 4: change_battle_menu_kind menu_node, "items"
    END SELECT
   END WITH
   EXIT DO
  END IF

  copypage holdscreen, vpage
  draw_menu menu, st, vpage
  setvispage vpage
  
  dowait
 LOOP

 freepage holdscreen
 setkeys
END SUB

SUB change_battle_menu_kind (byval menu_node as NodePtr, new_kind as string, init_integer as bool=NO)
 IF menu_node = 0 THEN debug "change_battle_menu_kind null NodePtr": EXIT SUB
 SELECT CASE new_kind
  CASE "weapon":
  CASE "attack":
  CASE "spells":
  CASE "items":
  CASE ELSE:
   debug "change_battle_menu_kind attempted to set invalid kind: """ & new_kind & """"
   EXIT SUB
 END SELECT
 DIM k as NodePtr
 k = menu_node."kind".ptr
 IF k."weapon".exists THEN FreeNode k."weapon".ptr
 IF k."attack".exists THEN FreeNode k."attack".ptr
 IF k."spells".exists THEN FreeNode k."spells".ptr
 IF k."items".exists THEN FreeNode k."items".ptr
 DIM n as NodePtr
 n = SetChildNode(k, new_kind)
 IF init_integer THEN
  SetContent n, 0
 END IF
END SUB

SUB update_hero_tags_menu (byref hero as HeroDef, menu() as string)
 WITH hero
  menu(0) = "Previous Menu: "
  menu(1) = "have hero: " + tag_condition_caption(.have_tag, "set tag", "NONE", "Not usable")
  menu(2) = "is alive: " + tag_condition_caption(.alive_tag, "set tag", "NONE", "Not usable")
  menu(3) = "is leader: " + tag_condition_caption(.leader_tag, "set tag", "NONE", "Not usable")
  menu(4) = "is in party now: " + tag_condition_caption(.active_tag, "set tag", "NONE", "Not usable")
 END WITH
END SUB

SUB hero_editor_tags (byval hero_id as integer, byref hero as HeroDef)
 DIM menu(5) as string
 DIM st as MenuState
 st.need_update = YES
 st.last = 4
 st.size = 24

 setkeys
 DO
  setwait 55
  setkeys
  IF keyval(scESC) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "hero_tags"
  usemenu st
  WITH hero
   SELECT CASE st.pt
    CASE 0
     IF enter_space_click(st) THEN EXIT DO
    CASE 1
     tag_grabber .have_tag, 0
    CASE 2
     tag_grabber .alive_tag, 0
    CASE 3
     tag_grabber .leader_tag, 0
    CASE 4
     tag_grabber .active_tag, 0
   END SELECT

   'Update cached hero tags
   herotags(hero_id).have_tag = .have_tag
   herotags(hero_id).alive_tag = .alive_tag
   herotags(hero_id).leader_tag = .leader_tag
   herotags(hero_id).active_tag = .active_tag
  END WITH

  clearpage dpage
  update_hero_tags_menu hero, menu()
  standardmenu menu(), st, 0, 0, dpage
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
END SUB

'This is not complete; it exists just to prevent crashes due to data
'corruption (eg. bug 871)
SUB enforce_hero_data_limits(her as HeroDef)
 clamp_value her.sprite, 0, gen(genMaxHeroPic), "hero sprite"
 clamp_value her.sprite_pal, -1, gen(genMaxPal), "hero sprite pal"
 clamp_value her.walk_sprite, 0, gen(genMaxNPCPic), "hero walkabout sprite"
 clamp_value her.walk_sprite_pal, -1, gen(genMaxPal), "hero walkabout sprite pal"
 clamp_value her.portrait, -1, gen(genMaxPortrait), "hero portrait"
 clamp_value her.portrait_pal, -1, gen(genMaxPal), "hero portrait pal"
 FOR i as integer = 0 TO 3
  clamp_value her.list_type(i), 0, 2, "hero spell list " & i & " type"
 NEXT i
END SUB

SUB update_hero_appearance_menu(byref st as HeroEditState, menu() as string, her as HeroDef)
 menu(1) = "Battle Picture: " & her.sprite
 menu(2) = "Battle Palette: " & defaultint(her.sprite_pal)
 menu(3) = "Walkabout Picture: " & her.walk_sprite
 menu(4) = "Walkabout Palette: " & defaultint(her.walk_sprite_pal)
 menu(5) = "Base Level: " & her.def_level
 IF her.def_level < 0 THEN menu(5) = "Base Level: Party Average"
 menu(6) = "Default Weapon: " & load_item_name(her.def_weapon, 0, 1)
 menu(7) = "Max Name Length: " & zero_default(her.max_name_len)
 menu(8) = "Hand position A..."
 menu(9) = "Hand position B..."
 menu(10) = "Portrait Picture: " & defaultint(her.portrait, "None")
 menu(11) = "Portrait Palette: " & defaultint(her.portrait_pal)
 update_hero_preview_pics st, her
 st.changed = NO
END SUB

SUB update_hero_preview_pics(byref st as HeroEditState, her as HeroDef)
 clear_hero_preview_pics st
 WITH st
  .battle.sprite    = frame_load(0, her.sprite)
  .battle.pal       = palette16_load(her.sprite_pal, 0, her.sprite)
  .walkabout.sprite = frame_load(4, her.walk_sprite)
  .walkabout.pal    = palette16_load(her.walk_sprite_pal, 4, her.walk_sprite)
  IF her.portrait >= 0 THEN
   .portrait.sprite = frame_load(8, her.portrait)
   .portrait.pal    = palette16_load(her.portrait_pal, 8, her.portrait)
  END IF
 END WITH
END SUB

SUB clear_hero_preview_pics(byref st as HeroEditState)
 WITH st
  IF .battle.sprite    THEN frame_unload    @.battle.sprite
  IF .battle.pal       THEN palette16_unload @.battle.pal
  IF .walkabout.sprite THEN frame_unload    @.walkabout.sprite
  IF .walkabout.pal    THEN palette16_unload @.walkabout.pal
  IF .portrait.sprite  THEN frame_unload    @.portrait.sprite
  IF .portrait.pal     THEN palette16_unload @.portrait.pal
 END WITH
END SUB

SUB draw_hero_preview(st as HeroEditState, her as HeroDef)
 STATIC tog as integer
 tog = loopvar(tog, 0, 3)
 
 DIM frame as integer
 IF st.previewframe <> -1 THEN
  frame = st.previewframe + 2
 ELSE
  frame = tog \ 2
 END IF
 frame_draw st.battle.sprite + frame, st.battle.pal, 250, 25,,,dpage
 frame = st.preview_walk_direction * 2 + tog \ 2
 frame_draw st.walkabout.sprite + frame, st.walkabout.pal, 230 + st.preview_walk_pos.x, 5 + st.preview_walk_pos.y, , , dpage
 DIM hand as XYPair
 DIM col as integer = uilook(uiSelectedItem + tog \ 2)
 IF st.previewframe <> -1 THEN
  hand.x = her.hand_pos(st.previewframe).x
  hand.y = her.hand_pos(st.previewframe).y
  drawline 248 + hand.x,25 + hand.y,249 + hand.x, 25 + hand.y, col, dpage
  drawline 250 + hand.x,23 + hand.y,250 + hand.x, 24 + hand.y, col, dpage
  drawline 251 + hand.x,25 + hand.y,252 + hand.x, 25 + hand.y, col, dpage
  drawline 250 + hand.x,26 + hand.y,250 + hand.x, 27 + hand.y, col, dpage
 END IF
 IF st.portrait.sprite THEN frame_draw st.portrait.sprite, st.portrait.pal, 240, 110, , , dpage
END SUB

SUB animate_hero_preview(byref st as HeroEditState)
 WITH st
  .preview_steps += 1
  IF .preview_steps >= 15 THEN
   .preview_steps = 0
   .preview_walk_direction = loopvar(.preview_walk_direction, 0, 3, 1)
  END IF
  IF .preview_walk_direction = 0 THEN .preview_walk_pos.y -= 4
  IF .preview_walk_direction = 1 THEN .preview_walk_pos.x += 4
  IF .preview_walk_direction = 2 THEN .preview_walk_pos.y += 4
  IF .preview_walk_direction = 3 THEN .preview_walk_pos.x -= 4
 END WITH
END SUB

SUB hero_editor_appearance(byref st as HeroEditState, byref her as HeroDef)
 
 DIM menu(11) as string
 DIM min(11) as integer
 DIM max(11) as integer
 menu(0) = "Previous Menu"
 min(1) = 0: max(1) = gen(genMaxHeroPic)
 min(2) = -1: max(2) = 32767
 min(3) = 0: max(3) = gen(genMaxNPCPic)
 min(4) = -1: max(4) = 32767
 min(5) = -1: max(5) = 99
 min(6) = 0: max(6) = gen(genMaxItem)
 min(7) = 0: max(7) = 16
 min(8) = -100:max(8) = 100
 min(9) = -100:max(9) = 100
 min(10) = -1:max(10) = gen(genMaxPortrait)
 min(11) = -1:max(11) = 32767

 DIM state as MenuState
 WITH state
  .pt = 0
  .last = UBOUND(menu)
  .size = 24
 END WITH

 update_hero_appearance_menu st, menu(), her
 st.changed = NO
 setkeys
 DO
  setwait 55
  setkeys
  animate_hero_preview st
  IF keyval(scEsc) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "hero_appearance"
  usemenu state
  st.previewframe = -1
  IF state.pt = 8 THEN st.previewframe = 0
  IF state.pt = 9 THEN st.previewframe = 1
  IF enter_space_click(state) AND state.pt = 0 THEN EXIT DO
  IF state.pt > 0 THEN
   SELECT CASE state.pt
    CASE 1
     IF intgrabber(her.sprite, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 2
     IF intgrabber(her.sprite_pal, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 3
     IF intgrabber(her.walk_sprite, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 4
     IF intgrabber(her.walk_sprite_pal, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 5
     IF intgrabber(her.def_level, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 6
     IF intgrabber(her.def_weapon, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 7
     IF intgrabber(her.max_name_len, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 10
     IF intgrabber(her.portrait, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 11
     IF intgrabber(her.portrait_pal, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
   END SELECT
   IF enter_space_click(state) THEN
    SELECT CASE state.pt
     CASE 2
      her.sprite_pal = pal16browse(her.sprite_pal, 0, her.sprite)
     CASE 4
      her.walk_sprite_pal = pal16browse(her.walk_sprite_pal, 4, her.walk_sprite)
     CASE 8
      xy_position_on_sprite st.battle, her.hand_pos(0).x, her.hand_pos(0).y, 2, 32, 40, "hand position (for weapon)", "xy_hero_hand"
     CASE 9
      xy_position_on_sprite st.battle, her.hand_pos(1).x, her.hand_pos(1).y, 3, 32, 40, "hand position (for weapon)", "xy_hero_hand"
     CASE 11
      her.portrait_pal = pal16browse(her.portrait_pal, 8, her.portrait)
    END SELECT
    st.changed = YES
   END IF
  END IF

  IF st.changed THEN update_hero_appearance_menu st, menu(), her

  clearpage dpage
  standardmenu menu(), state, 8, 0, dpage
  draw_hero_preview st, her
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
 st.previewframe = -1
END SUB

SUB hero_editor_equipment_list (byval hero_id as integer, byref her as HeroDef)
 DIM menu(5) as string
 DIM state as MenuState
 WITH state
  .last = 5
  .size = 22
 END WITH
 menu(0) = "Previous menu"
 menu(1) = her.name & "'s " & readglobalstring(38, "Weapon", 10) & " items"
 FOR i as integer = 0 TO 3
  menu(2+i) = her.name & "'s " & readglobalstring(25+i, "Armor " & (1+i), 10) & " items"
 NEXT i
 
 setkeys
 DO
  setwait 55
  setkeys
  IF keyval(scEsc) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "hero_equipment"
  usemenu state
  IF enter_space_click(state) THEN
   IF state.pt = 0 THEN
    EXIT DO
   ELSE
    hero_editor_equipbits hero_id, state.pt
   END IF
  END IF
  clearpage dpage
  standardmenu menu(), state, 0, 0, dpage
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
END SUB

SUB hero_editor_equipbits (byval hero_id as integer, byval equip_type as integer)
 '--equip_type is 0 for none (which would be silly) 1 for weapons and 2-5 for armor
 DIM tempbits(gen(genMaxItem) \ 16 + 1) as integer 
 DIM itemname(gen(genMaxItem)) as string
 DIM item_id(gen(genMaxItem)) as integer
 DIM itembuf(dimbinsize(binITM)) as integer
 DIM nextbit as integer = 0
 FOR i as integer = 0 TO gen(genMaxItem)
  loaditemdata itembuf(), i
  IF itembuf(49) = equip_type THEN
   itemname(nextbit) = readitemname(i)
   item_id(nextbit) = i
   setbit tempbits(), 0, nextbit, readbit(itembuf(), 66, hero_id)
   nextbit += 1
  END IF
 NEXT i
 editbitset tempbits(), 0, nextbit-1, itemname()
 FOR i as integer = 0 TO nextbit-1
  loaditemdata itembuf(), item_id(i)
  setbit itembuf(), 66, hero_id, readbit(tempbits(), 0, i)
  saveitemdata itembuf(), item_id(i)
 NEXT i
END SUB

'This elemental resistance editor is shared by the hero and item editors
SUB common_elementals_editor(elementals() as single, helpfile as string, byval showsign as integer = 0)
 DIM elementnames() as string
 getelementnames elementnames()
 DIM float_reprs(gen(genNumElements) - 1) as string
 DIM menu(1 + gen(genNumElements) - 1) as string
 DIM menu_display(UBOUND(menu)) as string
 DIM selectst as SelectTypeState
 DIM st as MenuState
 st.last = UBOUND(menu)
 st.size = 24
 st.need_update = YES

 FOR i as integer = 0 TO gen(genNumElements) - 1
  float_reprs(i) = format_percent(elementals(i))
  elementnames(i) = rpad(elementnames(i), " ", 15)
 NEXT

 DO
  setwait 55
  setkeys YES
  IF keyval(scEsc) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help helpfile
  IF st.pt = 0 THEN
   IF enter_space_click(st) THEN EXIT DO
  ELSE
   IF percent_grabber(elementals(st.pt - 1), float_reprs(st.pt - 1), -1000, 1000) THEN st.need_update = YES
  END IF
  usemenu st

  IF st.need_update THEN
   st.need_update = NO
   menu(0) = "Previous Menu"
   FOR i as integer = 0 TO gen(genNumElements) - 1
    menu(i + 1) = "Damage from " + elementnames(i) + ": "
    IF showsign THEN
     'positive values get explicit + prefix
     IF LEFT(float_reprs(i), 1) <> "-" THEN menu(i + 1) += "+"
    END IF
    menu(i + 1) += float_reprs(i)
   NEXT
  END IF
  IF select_by_typing(selectst, NO) THEN
   select_on_word_boundary menu(), selectst, st
  END IF

  clearpage vpage
  highlight_menu_typing_selection menu(), menu_display(), selectst, st
  standardmenu menu_display(), st, 0, 0, vpage
  setvispage vpage
  dowait
 LOOP
 setkeys
END SUB

SUB hero_editor_elementals(byref her as HeroDef)
 common_elementals_editor her.elementals(), "hero_elementals"
END SUB

'--Item Editor stuff---------------------------------------------------

SUB item_editor_equipbits(itembuf() as integer)
 DIM ibit(-1 TO maxMaxHero) as string
 FOR i as integer = 0 TO gen(genMaxHero)
  ibit(i) = "Equipable by " & getheroname(i)
 NEXT i
 editbitset itembuf(), 66, gen(genMaxHero), ibit()
END SUB

SUB item_editor_elementals(itembuf() as integer)
 DIM elementals(gen(genNumElements) - 1) as single
 FOR i as integer = 0 TO gen(genNumElements) - 1
  elementals(i) = DeSerSingle(itembuf(), 82 + i * 2)
  IF gen(genEquipMergeFormula) = 2 THEN  'additive merging
   elementals(i) -= 1.0
  END IF
 NEXT
 common_elementals_editor elementals(), "item_elementals", (gen(genEquipMergeFormula) = 2)
 FOR i as integer = 0 TO gen(genNumElements) - 1
  IF gen(genEquipMergeFormula) = 2 THEN  'additive merging
   elementals(i) += 1.0
  END IF
  SerSingle itembuf(), 82 + i * 2, elementals(i)
 NEXT
END SUB

SUB item_editor_init_new(itembuf() as integer)
 flusharray itembuf(), dimbinsize(binITM), 0
 FOR i as integer = 0 TO maxElements - 1
  SerSingle itembuf(), 82 + i * 2, 1.0
 NEXT i
END SUB

SUB item_editor_stat_bonuses(itembuf() as integer)
 DIM menu(-1 TO statLast) as string
 DIM menu_display(-1 TO statLast) as string
 menu(-1) = "Previous Menu"
 DIM selectst as SelectTypeState
 DIM state as MenuState
 state.first = -1
 state.top = -1
 state.last = UBOUND(menu)
 state.size = 24
 state.need_update = YES

 DIM sbmax(statLast) as integer
 FOR i as integer = 0 TO 1
  sbmax(i) = 9999
 NEXT i
 FOR i as integer = 2 TO 8
  sbmax(i) = 999
 NEXT i
 FOR i as integer = 9 TO 10
  sbmax(i) = 100
 NEXT i
 sbmax(11) = 10

 setkeys YES
 DO
  setwait 55
  setkeys YES
  IF keyval(scESC) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "equipment_stat_bonuses"
  usemenu state
  IF enter_space_click(state) THEN
   IF state.pt = -1 THEN EXIT DO
  END IF
  IF state.pt >= 0 THEN
   IF intgrabber(itembuf(54 + state.pt), sbmax(state.pt) * -1, sbmax(state.pt)) THEN
    state.need_update = YES
   END IF
  END IF

  IF state.need_update THEN
   state.need_update = NO
   FOR i as integer = 0 TO statLast
    menu(i) = statnames(i) + " Bonus: " & itembuf(54 + i)
   NEXT
  END IF
  IF select_by_typing(selectst, NO) THEN
   select_on_word_boundary menu(), selectst, state
  END IF

  clearpage dpage
  highlight_menu_typing_selection menu(), menu_display(), selectst, state
  standardmenu menu_display(), state, 0, 0, dpage
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
END SUB
